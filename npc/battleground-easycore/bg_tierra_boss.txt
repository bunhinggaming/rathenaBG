//===== rAthena Script =======================================
//    Extended Battleground
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
// Date:
//  10-10-2019
// By:
//  Easycore         (Zephyrus, original author)
//
// Contact:
//    - Facebook: https://www.facebook.com/EasycoreRo/
//    - rAthena:  https://rathena.org/board/profile/16425-easycore/
//    - Discord:  Easycore#3412
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 

// Battleground Queue Engine
// *********************************************************************
-	script	BOSS_BG_Queue	-1,{
	end;
	
OnInit:
	// Configuration
	.Num = 7;
	set .BG_Queue, bg_queue_create("Bossnia",strnpcinfo(0)+"::OnJoinEvent",getvariableofnpc(.BG_level[.Num],"BG_Queue_Join"));
	.BG_Name$ = getvariableofnpc(.BG_Names$[.Num],"BG_Queue_Join");
	.BG_Arena$ = getvariableofnpc(.BG_Arenas$[.Num],"BG_Queue_Join");
	.BG_Min = getvariableofnpc(.BG_Min[.Num],"BG_Queue_Join");
	.BG_Max = getvariableofnpc(.BG_Max[.Num],"BG_Queue_Join");
	.TeamMode = getvariableofnpc(.TeamMode,"BG_Queue_Join");
	end;
	
	
OnJoinBG:
	if (getcharid(6) == .BG_Queue)
		end;
	if (getbattleflag("bg_queue_interface") == 0) {
		message strcharinfo(0),"Queue UI are currently disabled.";
		end;
	} else if (getbattleflag("bg_eAmod_mode") == 0) {
		message strcharinfo(0),"Battlegrounds are currently disabled.";
		end;
	} else if( agitcheck() || agitcheck2() ) {
		dispbottom "[Battleground is currently disabled for this moment due to War of Emperium.]";
		end;
	} else if (!getvariableofnpc(.Is_Enabled[.Num],"BG_Queue_Join")) {
		message strcharinfo(0),"This arena is currently disabled.";
		end;
	} else if (getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,0)) {
		dispbottom "[You are a Deserter. Time remaining: "+(getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,5)/1000)+" seconds]";
		end;
	}
	if (getcharid(6))
		bg_queue_leaveall();
	bg_queue_join .BG_Queue;
	end;
OnDoLeave: // @leavebg
	$@BG_User_Min = .BG_Min*2;
	bg_queue_leave .BG_Queue;
	end;

OnJoinEvent:
	if( $@BG_Status_BOSS == 2 )
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch( $@BG_Status_BOSS ) {
		case 0: // Idle - Ready to Start

			if( bg_queue_checkstart(.BG_Queue,.TeamMode,2,.BG_Min) ) {
				donpcevent .BG_Arena$+"::OnBuildTeams";

				// Fill Teams with Queue Members
				bg_queue2teams .BG_Queue,.BG_Min,.BG_Max,.TeamMode,$@BG_BOSS_Team1,$@BG_BOSS_Team2;

				set $@BG_Status_BOSS,1;
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- Begining";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");

				donpcevent .BG_Arena$+"::OnReady";
			} else {
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- " + (.BG_Min*2 - .@Queue_Count) + " more players to start.";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
		case 1: // Running - Others can join
			if( .@Queue_Count > 0 ) {
				bg_balance_teams .BG_Queue,.BG_Max,.TeamMode,$@BG_BOSS_Team1,$@BG_BOSS_Team2;
				set .@BG_Count1, bg_get_data($@BG_BOSS_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_BOSS_Team2,0);
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- B: " + .@BG_Count1 + "/" + .BG_Max + ", R: " + .@BG_Count2 + "/" + .BG_Max + " (Playing)";

				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
	}
	end;
}
// ==============================================================================
// BattleGround System - Tierra Bossnia
// ==============================================================================*/

-	script	Tierra_Boss	-1,{
	end;

OnGuillaumeQuit:
OnCroixQuit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join"),0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_BOSS != 0)
		donpcevent "BOSS_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_Boss")
		donpcevent "BG_Queue_Join::OnJoinEvent";
	end;

OnBuildTeams:
	set getd(.T1$),bg_create("bat_cc2",88,139,0,"Tierra_Boss::OnGuillaumeQuit","");
	set getd(.T2$),bg_create("bat_cc2",96,139,1,"Tierra_Boss::OnCroixQuit","");
	end;

OnReady:
	if (!getbattleflag("bg_queue_interface") && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_Boss")
		end;

	initnpctimer;
	set .guillaume_boss, 0;
	set .croix_boss, 0;
	set .Neutral_Base, 0;
	sleep 2000;
	bg_warp getd(.T1$),"bat_cc2",61,120;
	bg_warp getd(.T2$),"bat_cc2",138,63;
	sleep 2000;
	donpcevent "#gtb_respawn::OnBGStart";
	donpcevent "#ctb_respawn::OnBGStart";
	bg_updatescore "bat_cc2",.croix_boss,.guillaume_boss;
	sleep 3000;
	donpcevent "Balance_Flag::OnBGStart";
	donpcevent "Guillaume_Boss::OnSummon";
	donpcevent "Croix_Boss::OnSummon";
	donpceventall "OnEmblemBOSS";
	mapannounce "bat_cc2","-- Capture the Balance Flag to remove immunity of Enemy Guardians --",1,0x483D8B;
	end;

OnNeutralReset:
	set .Neutral_Base, 0;
	killmonster "bat_cc2","Tierra_Boss::OnNeutralBreak";
	bg_monster_immunity getvariableofnpc(.Boss,"Guillaume_Boss"),1;
	bg_monster_immunity getvariableofnpc(.Boss,"Croix_Boss"),1;
	set .Flag, bg_monster(0,"bat_cc2",100,92,"Balance Flag",20403,"Tierra_Boss::OnNeutralBreak");
	sleep 2000;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_BOSS != 1)
		end;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_Boss"))
		end;
	mapannounce "bat_cc2","-- Balance Flag returned to normality --",1,0x483D8B;
	end;

OnNeutralBreak:
	if (getbattleflag("bg_queue_interface") && $@BG_Status_BOSS != 1)
		end;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_Boss"))
		end;
	set .Neutral_Base, getcharid(4);
	if (getcharid(4) == getd(.T1$)) {
		mapannounce "bat_cc2","-- Guillaume captured the Balance Flag. Croix guardian is now vulnerable to attacks!! --",1,0x0000FF;
		bg_monster_immunity getvariableofnpc(.Boss,"Guillaume_Boss"),1;
		bg_monster_immunity getvariableofnpc(.Boss,"Croix_Boss"),0;
		set .Flag, bg_monster(.Neutral_Base,"bat_cc2",100,92,"Guillaume Flag",20404,"Tierra_Boss::OnNeutralBreak");
		setunitdata(.Flag, UMOB_GROUP_ID, 100);
	} else if (getcharid(4) == getd(.T2$)) {
		mapannounce "bat_cc2","-- Croix captured the Balance Flag. Guillaume guardian is now vulnerable to attacks!! --",1,0xFF0000;
		bg_monster_immunity getvariableofnpc(.Boss,"Guillaume_Boss"),0;
		bg_monster_immunity getvariableofnpc(.Boss,"Croix_Boss"),1;
		set .Flag, bg_monster(.Neutral_Base,"bat_cc2",100,92,"Croix Flag",20405,"Tierra_Boss::OnNeutralBreak");
		setunitdata(.Flag, UMOB_GROUP_ID, 101);
	} else {
		set .Neutral_Base, 0;
		bg_monster_immunity getvariableofnpc(.Boss,"Guillaume_Boss"),1;
		bg_monster_immunity getvariableofnpc(.Boss,"Croix_Boss"),1;
		set .Flag, bg_monster(0,"bat_cc2",100,92,"Balance Flag",20403,"Tierra_Boss::OnNeutralBreak");
	}
	end;

OnTimer600000:
	mapannounce "bat_cc2","Battle of Tierra Bossnia will end in 5 minutes",1,0x483D8B;
	end;
OnTimer840000:
	mapannounce "bat_cc2","Battle of Tierra Bossnia will end in 1 minute",1,0x483D8B;
	end;

OnTimer900000:
OnMatchEnd:
	stopnpctimer;
	stopnpctimer "Guillaume_Boss";
	stopnpctimer "Croix_Boss";
	donpcevent "#gtb_respawn::OnBGStop";
	donpcevent "#ctb_respawn::OnBGStop";
	donpcevent "Balance_Flag::OnBGStop";
	killmonster "bat_cc2","Guillaume_Boss::OnBoss";
	killmonster "bat_cc2","Croix_Boss::OnBoss";
	if (getbattleflag("bg_queue_interface"))
		set $@BG_Status_BOSS, 2;
	else
		set $@BG_Status, 2;
	// =======================================================
	// Team Rewards
	// =======================================================
	if (.guillaume_boss < .croix_boss) {
		bg_reward getd(.T1$),7828,15 + getvariableofnpc(.croix_boss, "Tierra_Boss"),0,0,"BG_Boss",1,1,0;
		bg_reward getd(.T2$),7828,10 + getvariableofnpc(.guillaume_boss, "Tierra_Boss"),0,0,"BG_Boss",1,1,2;
		mapannounce "bat_cc2","The Guillaume army has won the Battle of Tierra Bossnia!",1,0x0000FF;
	} else if (.guillaume_boss > .croix_boss) {
		bg_reward getd(.T1$),7828,10 + getvariableofnpc(.croix_boss, "Tierra_Boss"),0,0,"BG_Boss",1,1,2;
		bg_reward getd(.T2$),7828,15 + getvariableofnpc(.guillaume_boss, "Tierra_Boss"),0,0,"BG_Boss",1,1,0;
		mapannounce "bat_cc2","The Croix army has won the Battle of Tierra Bossnia!",1,0xFF0000;
	} else {
		bg_reward getd(.T1$),7828,10 + getvariableofnpc(.croix_boss, "Tierra_Boss"),0,0,"BG_Boss",1,1,1;
		bg_reward getd(.T2$),7828,10 + getvariableofnpc(.guillaume_boss, "Tierra_Boss"),0,0,"BG_Boss",1,1,1;
		mapannounce "bat_cc2","The battle is over. This is a Tie...!",1,0x483D8B;
	}
	// =======================================================
	set .Neutral_Base, 0;
	set .guillaume_boss, 0;
	set .croix_boss, 0;
	donpcevent "BG_Queue_Join::OnEndArena";
	bg_updatescore "bat_cc2",.croix_boss,.guillaume_boss;
	end;
OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_BOSS_Team1";
		.T2$ = "$@BG_BOSS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

-	script	Guillaume_Boss	-1,{
	end;

OnSummon:
	set .@Boss, 21406 + getvariableofnpc(.guillaume_boss, "Tierra_Boss");
	mapannounce "bat_cc2","-- Guillaume Guardian [" + strmobinfo(2,.@Boss) + "] entering the battle --",1,0x0000FF;
	set .Boss, bg_monster(getd(.T1$),"bat_cc2",53,128,"Guillaume Guardian",.@Boss,"Guillaume_Boss::OnBoss");
	bg_monster_immunity .Boss,1;
	setunitdata(.Boss, UMOB_GROUP_ID, 100);
	switch(.@Boss) {
		case 21406: setunittitle(.Boss,"[First Guardian]"); break;
		case 21407: setunittitle(.Boss,"[Second Guardian]"); break;
		case 21408: setunittitle(.Boss,"[Last Guardian]"); break;
	}
	initnpctimer;
	end;

OnTimer2000:
	stopnpctimer;
	if (.Boss) {
		bg_monster_reveal .Boss,1,0x0000FF;
		getunitdata(.Boss, .@mobdata);
		if (.@mobdata[UMOB_TARGETID] == 0) {
			unitwalk(.Boss,54,127);
			.btick++;
			if (.btick > 5 && distance(54,127,.@mobdata[UMOB_X],.@mobdata[UMOB_Y]) > 6) {
				unitwarp(.Boss, "this", 54, 127);
				.btick = 0;
			} else if (.btick > 5)
				.btick = 0;
		} else
			.btick = 0;
		initnpctimer;
	}
	end;

OnBoss:
	stopnpctimer;
	bg_monster_reveal .Boss,2,0x0000FF; // Hide Spot
	set getvariableofnpc(.guillaume_boss, "Tierra_Boss"), getvariableofnpc(.guillaume_boss, "Tierra_Boss") + 1;
	bg_updatescore "bat_cc2",getvariableofnpc(.croix_boss,"Tierra_Boss"),getvariableofnpc(.guillaume_boss,"Tierra_Boss");
	set .Boss, 0;
	if (getvariableofnpc(.guillaume_boss, "Tierra_Boss") > 2)
		donpcevent "Tierra_Boss::OnMatchEnd";
	else
	{
		donpcevent "Tierra_Boss::OnNeutralReset";
		donpcevent "Guillaume_Boss::OnSummon";
	}
	end;
OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_BOSS_Team1";
		.T2$ = "$@BG_BOSS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

-	script	Croix_Boss	-1,{
	end;

OnSummon:
	set .@Boss, 21406 + getvariableofnpc(.croix_boss, "Tierra_Boss");
	mapannounce "bat_cc2","-- Croix Guardian [" + strmobinfo(2,.@Boss) + "] entering the battle --",1,0xFF0000;
	set .Boss, bg_monster(getd(.T2$),"bat_cc2",146,55,"Croix Guardian",.@Boss,"Croix_Boss::OnBoss");
	bg_monster_immunity .Boss,1;
	setunitdata(.Boss, UMOB_GROUP_ID, 101);
	switch(.@Boss) {
		case 21406: setunittitle(.Boss,"[First Guardian]"); break;
		case 21407: setunittitle(.Boss,"[Second Guardian]"); break;
		case 21408: setunittitle(.Boss,"[Last Guardian]"); break;
	}
	initnpctimer;
	end;

OnTimer2000:
	stopnpctimer;
	if (.Boss) {
		bg_monster_reveal .Boss,1,0xFF0000;
		getunitdata(.Boss, .@mobdata);
		if (.@mobdata[UMOB_TARGETID] == 0) {
			unitwalk(.Boss,146,55);
			if (.btick > 5 && distance(146,55,.@mobdata[UMOB_X],.@mobdata[UMOB_Y]) > 6) {
				unitwarp(.Boss, "this", 146, 55);
				.btick = 0;
			} else if (.btick > 5)
				.btick = 0;
		} else
			.btick = 0;
		initnpctimer;
	}
	end;

OnBoss:
	stopnpctimer;
	bg_monster_reveal .Boss,2,0xFF0000; // Hide Spot
	set getvariableofnpc(.croix_boss, "Tierra_Boss"), getvariableofnpc(.croix_boss, "Tierra_Boss") + 1;
	bg_updatescore "bat_cc2",getvariableofnpc(.croix_boss,"Tierra_Boss"),getvariableofnpc(.guillaume_boss,"Tierra_Boss");
	set .Boss, 0;
	if (getvariableofnpc(.croix_boss, "Tierra_Boss") > 2)
		donpcevent "Tierra_Boss::OnMatchEnd";
	else
	{
		donpcevent "Tierra_Boss::OnNeutralReset";
		donpcevent "Croix_Boss::OnSummon";
	}
	end;
OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_BOSS_Team1";
		.T2$ = "$@BG_BOSS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

-	script	Balance_Flag	-1,{
	end;

OnBGStart:
	viewpointmap "bat_cc2",1,100,92,1,0xFFFFFF;
	bg_monster 0,"bat_cc2",100,92,"Balance Flag",20403,"Tierra_Boss::OnNeutralBreak";
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	viewpointmap "bat_cc2",2,100,92,1,0xFFFFFF;
	killmonster "bat_cc2","Tierra_Boss::OnNeutralBreak";
	end;

OnTimer2000:
	initnpctimer;
	set .@NB, getvariableofnpc(.Neutral_Base,"Tierra_Boss");
	if (.@NB == getd(.T1$))
		viewpointmap "bat_cc2",1,100,92,1,0x0000FF;
	else if (.@NB == getd(.T2$))
		viewpointmap "bat_cc2",1,100,92,1,0xFF0000;
	else
		viewpointmap "bat_cc2",1,100,92,1,0xFFFFFF;
	end;
OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_BOSS_Team1";
		.T2$ = "$@BG_BOSS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

// MapFlags
// *********************************************************************

bat_cc2	mapflag	battleground	2
bat_cc2	mapflag	nomemo
bat_cc2	mapflag	nosave	SavePoint
bat_cc2	mapflag	noteleport
bat_cc2	mapflag	monster_noteleport
bat_cc2	mapflag	nowarp
bat_cc2	mapflag	nowarpto
bat_cc2	mapflag	noreturn
bat_cc2	mapflag	nobranch
bat_cc2	mapflag	nopenalty
bat_cc2	mapflag	noloot

// Other Flags
// *********************************************************************
bat_cc2,88,146,4	script	Flag#BOSS_Blue	GUILD_FLAG,{
	end;
OnEmblemBOSS:
	if (!getbattleflag("bg_queue_interface"))
		.@t$ = "$@BG_Team1";
	else
		.@t$ = "$@BG_BOSS_Team1";

	flagemblembg getd(.@t$);
	end;
}
bat_cc2,97,146,4	script	Flag#BOSS_Red	GUILD_FLAG,{
	end;
OnEmblemBOSS:
	if (!getbattleflag("bg_queue_interface"))
		.@t$ = "$@BG_Team2";
	else
		.@t$ = "$@BG_BOSS_Team2";

	flagemblembg getd(.@t$);
	end;
}

bat_cc2,139,53,1	duplicate(Base Flag#bg)	Croix Base#tb_1	974
bat_cc2,149,62,4	duplicate(Base Flag#bg)	Croix Base#tb_2	974
//bat_cc2,357,75,1	duplicate(Base Flag#bg)	Omega Base#tb_3	974
//bat_cc2,348,74,1	duplicate(Base Flag#bg)	Omega Base#tb_4	974
//bat_cc2,199,49,1	duplicate(Base Flag#bg)	Omega Base#tb_5	974
//bat_cc2,168,16,1	duplicate(Base Flag#bg)	Omega Base#tb_6	974
//bat_cc2,138,12,1	duplicate(Base Flag#bg)	Omega Base#tb_7	974
//bat_cc2,108,35,1	duplicate(Base Flag#bg)	Omega Base#tb_8	974
bat_cc2,50,121,1	duplicate(Base Flag#bg)	Guillaume Base#tb_1	973
bat_cc2,60,131,1	duplicate(Base Flag#bg)	Guillaume Base#tb_2	973
//bat_cc2,359,327,1	duplicate(Base Flag#bg)	Alpha Base#tb_3	973
//bat_cc2,350,326,1	duplicate(Base Flag#bg)	Alpha Base#tb_4	973
//bat_cc2,209,344,1	duplicate(Base Flag#bg)	Alpha Base#tb_5	973
//bat_cc2,173,380,1	duplicate(Base Flag#bg)	Alpha Base#tb_6	973
//bat_cc2,150,380,1	duplicate(Base Flag#bg)	Alpha Base#tb_7	973
//bat_cc2,118,357,1	duplicate(Base Flag#bg)	Alpha Base#tb_8	973
//bat_cc2,119,336,1	duplicate(Base Flag#bg)	Alpha Base#tb_9	973

// Battleground Respawn
// *********************************************************************

bat_cc2,88,143,5	script	Therapist in battle#boss1	4_F_SISTER,{
	getmapxy(.@map$,.@x,.@y, BL_PC);
	if (distance(.@x,.@y,88,143) > 6)
		end;
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
OnInit:
	setunittitle(getnpcid(0), "[Blue Team]");
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 100);
	end;
}

bat_cc2,97,143,3	script	Therapist in battle#boss2	4_F_SISTER,{
	getmapxy(.@map$,.@x,.@y, BL_PC);
	if (distance(.@x,.@y,97,143) > 6)
		end;
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
OnInit:
	setunittitle(getnpcid(0), "[Red Team]");
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 101);
	end;
}

bat_cc2,88,139,0	script	#gtb_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_cc2",85,134,92,146,100,100;
	areawarp "bat_cc2",85,134,92,146,"bat_cc2",54,127;
	initnpctimer;
	end;
}

bat_cc2,96,139,0	script	#ctb_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_cc2",93,134,100,146,100,100;
	areawarp "bat_cc2",93,134,100,146,"bat_cc2",146,55;
	initnpctimer;
	end;
}