//===== rAthena Script =======================================
//    Extended Battleground
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
// Date:
//  10-10-2019
// By:
//  Easycore         (Zephyrus, original author)
//
// Contact:
//    - Facebook: https://www.facebook.com/EasycoreRo/
//    - rAthena:  https://rathena.org/board/profile/16425-easycore/
//    - Discord:  Easycore#3412
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 

// ==============================================================================
// BattleGround System - Tierra Domination
// ==============================================================================

// Battleground Engine
// *********************************************************************

// Battleground Queue Engine
// *********************************************************************
-	script	DOM_BG_Queue	-1,{
	end;
	
OnInit:
	// Configuration
	.Num = 6;
	set .BG_Queue, bg_queue_create("Domination",strnpcinfo(0)+"::OnJoinEvent",getvariableofnpc(.BG_level[.Num],"BG_Queue_Join"));
	.BG_Name$ = getvariableofnpc(.BG_Names$[.Num],"BG_Queue_Join");
	.BG_Arena$ = getvariableofnpc(.BG_Arenas$[.Num],"BG_Queue_Join");
	.BG_Min = getvariableofnpc(.BG_Min[.Num],"BG_Queue_Join");
	.BG_Max = getvariableofnpc(.BG_Max[.Num],"BG_Queue_Join");
	.TeamMode = getvariableofnpc(.TeamMode,"BG_Queue_Join");
	end;
	
	
OnJoinBG:
	if (getcharid(6) == .BG_Queue)
		end;
	if (getbattleflag("bg_queue_interface") == 0) {
		message strcharinfo(0),"Queue UI are currently disabled.";
		end;
	} else if (getbattleflag("bg_eAmod_mode") == 0) {
		message strcharinfo(0),"Battlegrounds are currently disabled.";
		end;
	} else if( agitcheck() || agitcheck2() ) {
		dispbottom "[Battleground is currently disabled for this moment due to War of Emperium.]";
		end;
	} else if (!getvariableofnpc(.Is_Enabled[.Num],"BG_Queue_Join")) {
		message strcharinfo(0),"This arena is currently disabled.";
		end;
	} else if (getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,0)) {
		dispbottom "[You are a Deserter. Time remaining: "+(getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,5)/1000)+" seconds]";
		end;
	}
	if (getcharid(6))
		bg_queue_leaveall();
	$@BG_User_Min = .BG_Min*2;
	bg_queue_join .BG_Queue;
	end;
OnDoLeave: // @leavebg
	bg_queue_leave .BG_Queue;
	end;

OnJoinEvent:
	if( $@BG_Status_DOM == 2 )
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch( $@BG_Status_DOM ) {
		case 0: // Idle - Ready to Start

			if( bg_queue_checkstart(.BG_Queue,.TeamMode,2,.BG_Min) ) {
				donpcevent .BG_Arena$+"::OnBuildTeams";

				// Fill Teams with Queue Members
				bg_queue2teams .BG_Queue,.BG_Min,.BG_Max,.TeamMode,$@BG_DOM_Team1,$@BG_DOM_Team2;

				set $@BG_Status_DOM,1;
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- Begining";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");

				donpcevent .BG_Arena$+"::OnReady";
			} else {
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- " + (.BG_Min*2 - .@Queue_Count) + " more players to start.";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
		case 1: // Running - Others can join
			if( .@Queue_Count > 0 ) {
				bg_balance_teams .BG_Queue,.BG_Max,.TeamMode,$@BG_DOM_Team1,$@BG_DOM_Team2;
				set .@BG_Count1, bg_get_data($@BG_DOM_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_DOM_Team2,0);
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- B: " + .@BG_Count1 + "/" + .BG_Max + ", R: " + .@BG_Count2 + "/" + .BG_Max + " (Playing)";

				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
	}
	end;
}
-	script	Tierra_DOM	FAKE_NPC,{
	end;

OnBuildTeams:
	set getd(.T1$),bg_create("bat_a04",50,374,0,"Tierra_DOM::OnTeam1Quit","Tierra_DOM::OnDie");
	set getd(.T2$),bg_create("bat_a04",42,16,1,"Tierra_DOM::OnTeam2Quit","Tierra_DOM::OnDie");
	end;

OnTeam1Quit:
OnTeam2Quit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join"),0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_DOM != 0)
		donpcevent "DOM_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_DOM")
		donpcevent "BG_Queue_Join::OnJoinEvent";
	end;

OnDie:
	// Check for Offensive or Defensive Kills
	if (getbattleflag("bg_queue_interface") && $@BG_Status_DOM != 1)
		end;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_DOM"))
		end;

	if (!killerrid || killerrid == getcharid(4))
		end;

	set .@Base, 0;
	getmapxy .@m$, .@x, .@y, BL_PC;

	if (.@x >= 136 && .@y >= 329 && .@x <= 186 && .@y <= 361)
		set .@Base, getvariableofnpc(.Owner,"Dom_N_Base"); // North
	else if (.@x >= 260 && .@y >= 194 && .@x <= 287 && .@y <= 213)
		set .@Base, getvariableofnpc(.Owner,"Dom_C_Base"); // Center
	else if (.@x >= 129 && .@y >= 34 && .@x <= 175 && .@y <= 65)
		set .@Base, getvariableofnpc(.Owner,"Dom_S_Base"); // South
	else end; // Not Killed on Base territory

	if (.@Base == 1)
	{ // Team 1
		if (getcharid(4) == getd(.T1$) && killerrid)
			bg_rankpoints "dom_off_kills",1,0,killerrid;
		else if (killerrid)
			bg_rankpoints "dom_def_kills",1,0,killerrid;
	}
	else if (.@Base == 2)
	{ // Team 2
		if (getcharid(4) == getd(.T2$) && killerrid)
			bg_rankpoints "dom_off_kills",1,0,killerrid;
		else if (killerrid)
			bg_rankpoints "dom_def_kills",1,0,killerrid;
	}
	end;

OnReady:
	if (!getbattleflag("bg_queue_interface") && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_DOM")
		end;

	set .Team1_Score, 0;
	set .Team2_Score, 0;
	bg_updatescore "bat_a04",.Team1_Score,.Team2_Score;
	donpcevent "Dom_N_Base::OnBase";
	donpcevent "Dom_S_Base::OnBase";
	donpcevent "Dom_C_Base::OnBase";
	sleep 2100;
	bg_warp getd(.T1$),"bat_a04",353,344;
	bg_warp getd(.T2$),"bat_a04",353,52;
	sleep 2100;
	donpcevent "#guiDOM_respawn::OnBGStart";
	donpcevent "#croDOM_respawn::OnBGStart";
	end;

OnNScore:
	if (set(.@North, getvariableofnpc(.Owner,"Dom_N_Base")) == 0)
		end; // No Owner

	if (.@North == 1)
		set .Team1_Score, .Team1_Score + 1;
	else
		set .Team2_Score, .Team2_Score + 1;

	donpcevent "Tierra_DOM::OnValidateScore";
	end;
	
OnSScore:
	if (set(.@South, getvariableofnpc(.Owner,"Dom_S_Base")) == 0)
		end; // No Owner

	if (.@South == 1)
		set .Team1_Score, .Team1_Score + 1;
	else
		set .Team2_Score, .Team2_Score + 1;

	donpcevent "Tierra_DOM::OnValidateScore";
	end;

OnCScore:
	if (set(.@Center, getvariableofnpc(.Owner,"Dom_C_Base")) == 0)
		end; // No Owner

	if (.@Center == 1)
		set .Team1_Score, .Team1_Score + 1;
	else
		set .Team2_Score, .Team2_Score + 1;

	donpcevent "Tierra_DOM::OnValidateScore";
	end;

OnValidateScore:
	if (.Team1_Score > 99)
		set .Team1_Score, 99;
	if (.Team2_Score > 99)
		set .Team2_Score, 99;

	bg_updatescore "bat_a04",.Team1_Score,.Team2_Score; // Update Visual Score

	if (.Team1_Score < 99 && .Team2_Score < 99)
		end; // No winners

OnMatchEnd:
	stopnpctimer "Dom_N_Base";
	stopnpctimer "Dom_S_Base";
	stopnpctimer "Dom_C_Base";
	donpcevent "#guiDOM_respawn::OnBGStop";
	donpcevent "#croDOM_respawn::OnBGStop";
	if (getbattleflag("bg_queue_interface"))
		set $@BG_Status_DOM, 2;
	else
		set $@BG_Status, 2;
	// =======================================================
	// Team Rewards
	// =======================================================
	.@reward_win = 5;
	.@reward_lose = 3;

	// Tie
	if (.Team1_Score >= 99 && .Team2_Score >= 99) {
		bg_reward getd(.T1$),7828,.@reward_lose,0,0,"BG_DOM",1,8,1;
		bg_reward getd(.T2$),7828,.@reward_lose,0,0,"BG_DOM",1,8,1;
		mapannounce "bat_a04","The battle is over. This is a Tie...!",1,0x4169E1;
	}
	// Team 1 Won
	else if (.Team1_Score >= 99) {
		bg_reward getd(.T1$),7828,.@reward_win,0,0,"BG_DOM",1,8,0;
		bg_reward getd(.T2$),7828,.@reward_lose,0,0,"BG_DOM",1,8,2;
		mapannounce "bat_a04","The " + bg_get_data(getd(.T1$),2) + " army has won the Battle of Tierra Domination!",1,bg_get_data(getd(.T1$),4);
	}
	// Team 2 Won
	else if (.Team2_Score >= 99) {
		bg_reward getd(.T1$),7828,.@reward_lose,0,0,"BG_DOM",1,8,2;
		bg_reward getd(.T2$),7828,.@reward_win,0,0,"BG_DOM",1,8,0;
		mapannounce "bat_a04","The " + bg_get_data(getd(.T2$),2) + " army has won the Battle of Tierra Domination!",1,bg_get_data(getd(.T2$),4);
	}
	// =======================================================
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 5000;
	donpcevent "BG_Queue_Join::OnEndArena";
	bg_updatescore "bat_a04",0,0;
	donpcevent "GDomNBW::OnDisable";
	donpcevent "GDomCBW::OnDisable";
	donpcevent "GDomSBW::OnDisable";
	donpcevent "CDomNBW::OnDisable";
	donpcevent "CDomCBW::OnDisable";
	donpcevent "CDomSBW::OnDisable";
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_DOM_Team1";
		.T2$ = "$@BG_DOM_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

// Battleground Bases
// *********************************************************************

bat_a04,173,345,0	script	North Base::Dom_N_Base	1911,{
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	set .Tick, 0;
	setnpcdisplay "Dom_N_Base","North Base",1911;
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_DOM"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_DOM != 1)
		end;

	set .@Team1Count, bg_getareausers(getd(.T1$),"bat_a04",136,329,186,361);
	set .@Team2Count, bg_getareausers(getd(.T2$),"bat_a04",136,329,186,361);
	set .Balance, .Balance + set(.@Balance, .@Team1Count - .@Team2Count);

	if (.Balance < -50)
		set .Balance, -50;
	else if (.Balance > 50)
		set .Balance, 50;

	switch(.Owner) {
		case 0:
			if (.Balance == 50) {
				set .Balance, 50;
				set .Owner, 1; // Team 1
				setnpcdisplay "Dom_N_Base",bg_get_data(getd(.T1$),2) + " Base",1912;
				mapannounce "bat_a04","North Base captured by " + bg_get_data(getd(.T1$),2),1,bg_get_data(getd(.T1$),4);
				unittalk(getnpcid(0),"Captured by Blue Team!");
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 100);
				bg_rankpoints_area getd(.T1$),"bat_a04",136,329,186,361,"dom_bases",1,$@dom_bases;
				donpcevent "GDomNBW::OnEnable";
			}
			else if (.Balance == -50) {
				set .Balance, -50;
				set .Owner, 2; // Team 2
				setnpcdisplay "Dom_N_Base",bg_get_data(getd(.T2$),2) + " Base",1913;
				mapannounce "bat_a04","North Base captured by " + bg_get_data(getd(.T2$),2),1,bg_get_data(getd(.T2$),4);
				unittalk(getnpcid(0),"Captured by Red Team!");
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 101);
				bg_rankpoints_area getd(.T2$),"bat_a04",136,329,186,361,"dom_bases",1,$@dom_bases;
				donpcevent "CDomNBW::OnEnable";
			}
			break;
		case 1:
			if (.Balance <= 0) { // Team 1 lost Control
				set .Owner, 0;
				setnpcdisplay "Dom_N_Base","North Base",1911;
				mapannounce "bat_a04",bg_get_data(getd(.T1$),2) + " lost control of the North Base",1,bg_get_data(getd(.T1$),4);
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
				donpcevent "GDomNBW::OnDisable";
			}
			break;
		case 2:
			if (.Balance >= 0) { // Team 2 lost Control
				set .Owner, 0;
				setnpcdisplay "Dom_N_Base","North Base",1911;
				mapannounce "bat_a04",bg_get_data(getd(.T2$),2) + " lost control of the North Base",1,bg_get_data(getd(.T2$),4);
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
				donpcevent "CDomNBW::OnDisable";
			}
			break;
	}
	if (.Balance < 0) {
		.@b = .Balance;
		.@b -= .@b*2;
	}

	if (.Balance > 0 && .Balance < 50 && .Owner == 0)
		unittalk(getnpcid(0),"Blue Team: "+(.Balance*2)+"%");
	else if (.Balance < 0 && .Balance > -50 && .Owner == 0)
		unittalk(getnpcid(0),"Red Team: "+(.@b*2)+"%");

	if (.@Balance > 0)
		specialeffect 236;
	else if (.@Balance < 0)
		specialeffect 225;
	else if (.Owner == 1)
		specialeffect 236;
	else if (.Owner == 2)
		specialeffect 225;
	else
		specialeffect 223;
	
	switch(.Owner) {
		case 0: viewpointmap "bat_a04",1,173,345,1,0xFFFFFF; break;
		case 1: viewpointmap "bat_a04",1,173,345,1,bg_get_data(getd(.T1$),4); break;
		case 2: viewpointmap "bat_a04",1,173,345,1,bg_get_data(getd(.T2$),4); break;
	}
	
	if (.Owner == 0)
		set .Tick, 0;
	else if (set(.Tick, .Tick + 1) == 7) {
		donpcevent "Tierra_DOM::OnNScore";
		set .Tick, 0;
	}

	initnpctimer;
	end;
OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_DOM_Team1";
		.T2$ = "$@BG_DOM_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

bat_a04,273,204,0	script	Center Base::Dom_C_Base	1911,{
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	set .Tick, 0;
	setnpcdisplay "Dom_C_Base","Center Base",1911;
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_DOM"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_DOM != 1)
		end;

	set .@Team1Count, bg_getareausers(getd(.T1$),"bat_a04",260,194,287,213);
	set .@Team2Count, bg_getareausers(getd(.T2$),"bat_a04",260,194,287,213);
	set .Balance, .Balance + set(.@Balance, .@Team1Count - .@Team2Count);

	if (.Balance < -50)
		set .Balance, -50;
	else if (.Balance > 50)
		set .Balance, 50;

	switch(.Owner) {
		case 0:
			if (.Balance == 50) {
				set .Balance, 50;
				set .Owner, 1; // Team 1
				setnpcdisplay "Dom_C_Base",bg_get_data(getd(.T1$),2) + " Base",1912;
				mapannounce "bat_a04","Center Base captured by " + bg_get_data(getd(.T1$),2),1,bg_get_data(getd(.T1$),4);
				unittalk(getnpcid(0),"Captured by Blue Team!");
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 100);
				bg_rankpoints_area getd(.T1$),"bat_a04",260,194,287,213,"dom_bases",1,$@dom_bases;
				donpcevent "GDomCBW::OnEnable";
			}
			else if (.Balance == -50) {
				set .Balance, -50;
				set .Owner, 2; // Team 2
				setnpcdisplay "Dom_C_Base",bg_get_data(getd(.T2$),2) + " Base",1913;
				mapannounce "bat_a04","Center Base captured by " + bg_get_data(getd(.T2$),2),1,bg_get_data(getd(.T2$),4);
				unittalk(getnpcid(0),"Captured by Red Team!");
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 101);
				bg_rankpoints_area getd(.T2$),"bat_a04",260,194,287,213,"dom_bases",1,$@dom_bases;
				donpcevent "CDomCBW::OnEnable";
			}
			break;
		case 1:
			if (.Balance <= 0) { // Team 1 lost Control
				set .Owner, 0;
				setnpcdisplay "Dom_C_Base","Center Base",1911;
				mapannounce "bat_a04",bg_get_data(getd(.T1$),2) + " lost control of the Center Base",1,bg_get_data(getd(.T1$),4);
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
				donpcevent "GDomCBW::OnDisable";
			}
			break;
		case 2:
			if (.Balance >= 0) { // Team 2 lost Control
				set .Owner, 0;
				setnpcdisplay "Dom_C_Base","Center Base",1911;
				mapannounce "bat_a04",bg_get_data(getd(.T2$),2) + " lost control of the Center Base",1,bg_get_data(getd(.T2$),4);
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
				donpcevent "CDomCBW::OnDisable";
			}
			break;
	}
	if (.Balance < 0) {
		.@b = .Balance;
		.@b -= .@b*2;
	}

	if (.Balance > 0 && .Balance < 50 && .Owner == 0)
		unittalk(getnpcid(0),"Blue Team: "+(.Balance*2)+"%");
	else if (.Balance < 0 && .Balance > -50 && .Owner == 0)
		unittalk(getnpcid(0),"Red Team: "+(.@b*2)+"%");

	if (.@Balance > 0)
		specialeffect 236;
	else if (.@Balance < 0)
		specialeffect 225;
	else if (.Owner == 1)
		specialeffect 236;
	else if (.Owner == 2)
		specialeffect 225;
	else
		specialeffect 223;
	
	switch(.Owner) {
		case 0: viewpointmap "bat_a04",1,273,204,3,0xFFFFFF; break;
		case 1: viewpointmap "bat_a04",1,273,204,3,bg_get_data(getd(.T1$),4); break;
		case 2: viewpointmap "bat_a04",1,273,204,3,bg_get_data(getd(.T2$),4); break;
	}
	
	if (.Owner == 0)
		set .Tick, 0;
	else if (set(.Tick, .Tick + 1) == 7) {
		donpcevent "Tierra_DOM::OnCScore";
		set .Tick, 0;
	}

	initnpctimer;
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_DOM_Team1";
		.T2$ = "$@BG_DOM_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

bat_a04,164,50,0	script	South Base::Dom_S_Base	1911,{
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	set .Tick, 0;
	setnpcdisplay "Dom_S_Base","South Base",1911;
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_DOM"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_DOM != 1)
		end;

	set .@Team1Count, bg_getareausers(getd(.T1$),"bat_a04",129,34,175,65);
	set .@Team2Count, bg_getareausers(getd(.T2$),"bat_a04",129,34,175,65);
	set .Balance, .Balance + set(.@Balance, .@Team1Count - .@Team2Count);

	if (.Balance < -50)
		set .Balance, -50;
	else if (.Balance > 50)
		set .Balance, 50;

	switch(.Owner) {
		case 0:
			if (.Balance == 50) {
				set .Balance, 50;
				set .Owner, 1; // Team 1
				setnpcdisplay "Dom_S_Base",bg_get_data(getd(.T1$),2) + " Base",1912;
				mapannounce "bat_a04","South Base captured by " + bg_get_data(getd(.T1$),2),1,bg_get_data(getd(.T1$),4);
				unittalk(getnpcid(0),"Captured by Blue Team!");
				bg_rankpoints_area getd(.T1$),"bat_a04",129,34,175,65,"dom_bases",1,$@dom_bases;
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 100);
				donpcevent "GDomSBW::OnEnable";
			}
			else if (.Balance == -50) {
				set .Balance, -50;
				set .Owner, 2; // Team 2
				setnpcdisplay "Dom_S_Base",bg_get_data(getd(.T2$),2) + " Base",1913;
				mapannounce "bat_a04","South Base captured by " + bg_get_data(getd(.T2$),2),1,bg_get_data(getd(.T2$),4);
				unittalk(getnpcid(0),"Captured by Red Team!");
				bg_rankpoints_area getd(.T2$),"bat_a04",129,34,175,65,"dom_bases",1,$@dom_bases;
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 101);
				donpcevent "CDomSBW::OnEnable";
			}
			break;
		case 1:
			if (.Balance <= 0) { // Team 1 lost Control
				set .Owner, 0;
				setnpcdisplay "Dom_S_Base","North Base",1911;
				mapannounce "bat_a04",bg_get_data(getd(.T1$),2) + " lost control of the South Base",1,bg_get_data(getd(.T1$),4);
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
				donpcevent "GDomSBW::OnDisable";
			}
			break;
		case 2:
			if (.Balance >= 0) { // Team 2 lost Control
				set .Owner, 0;
				setnpcdisplay "Dom_S_Base","North Base",1911;
				mapannounce "bat_a04",bg_get_data(getd(.T2$),2) + " lost control of the South Base",1,bg_get_data(getd(.T2$),4);
				setunitdata(getnpcid(0), UNPC_GROUP_ID, 0);
				donpcevent "CDomSBW::OnDisable";
			}
			break;
	}
	if (.Balance < 0) {
		.@b = .Balance;
		.@b -= .@b*2;
	}

	if (.Balance > 0 && .Balance < 50 && .Owner == 0)
		unittalk(getnpcid(0),"Blue Team: "+(.Balance*2)+"%");
	else if (.Balance < 0 && .Balance > -50 && .Owner == 0)
		unittalk(getnpcid(0),"Red Team: "+(.@b*2)+"%");

	if (.@Balance > 0)
		specialeffect 236;
	else if (.@Balance < 0)
		specialeffect 225;
	else if (.Owner == 1)
		specialeffect 236;
	else if (.Owner == 2)
		specialeffect 225;
	else
		specialeffect 223;
	
	switch(.Owner) {
		case 0: viewpointmap "bat_a04",1,164,50,2,0xFFFFFF; break;
		case 1: viewpointmap "bat_a04",1,164,50,2,bg_get_data(getd(.T1$),4); break;
		case 2: viewpointmap "bat_a04",1,164,50,2,bg_get_data(getd(.T2$),4); break;
	}

	if (.Owner == 0)
		set .Tick, 0;
	else if (set(.Tick, .Tick + 1) == 7) {
		donpcevent "Tierra_DOM::OnSScore";
		set .Tick, 0;
	}

	initnpctimer;
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_DOM_Team1";
		.T2$ = "$@BG_DOM_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_a04,53,377,3	script	Therapist in battle#DOM2	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

bat_a04,45,18,3	script	Therapist in battle#DOM1	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

// Battleground Respawn
// *********************************************************************

bat_a04,46,377,0	script	North Base Warp::GDomNBW	1_SHADOW_NPC,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a04",115,346,20;
	end;
}

bat_a04,46,370,0	script	Center Base Warp::GDomCBW	1_SHADOW_NPC,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a04",285,226,20;
	end;
}

bat_a04,53,370,0	script	South Base Warp::GDomSBW	1_SHADOW_NPC,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a04",106,48,20;
	end;
}

// *********************************************************************

bat_a04,38,19,0	script	North Base Warp::CDomNBW	1_SHADOW_NPC,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a04",115,346,20;
	end;
}

bat_a04,38,12,0	script	Center Base Warp::CDomCBW	1_SHADOW_NPC,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;
OnWarp:
	warpwaitingpc "bat_a04",260,183,20;
	end;
}

bat_a04,45,12,0	script	South Base Warp::CDomSBW	1_SHADOW_NPC,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a04",106,48,20;
	end;
}

// *********************************************************************

bat_a04,50,374,0	script	#guiDOM_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a04",46,370,54,378,100,100;
	donpcevent "GDomNBW::OnWarp";
	donpcevent "GDomCBW::OnWarp";
	donpcevent "GDomSBW::OnWarp";
	areawarp "bat_a04",46,370,54,378,"bat_a04",353,344;
	initnpctimer;
	end;
}

bat_a04,42,16,0	script	#croDOM_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a04",38,12,47,21,100,100;
	donpcevent "GDomNBW::OnWarp";
	donpcevent "GDomCBW::OnWarp";
	donpcevent "GDomSBW::OnWarp";
	areawarp "bat_a04",38,12,47,21,"bat_a04",353,52;
	initnpctimer;
	end;
}

// MapFlags
// *********************************************************************

bat_a04	mapflag	battleground	2
bat_a04	mapflag	nomemo
bat_a04	mapflag	nosave	SavePoint
bat_a04	mapflag	noteleport
bat_a04	mapflag	nowarp
bat_a04	mapflag	nowarpto
bat_a04	mapflag	noreturn
bat_a04	mapflag	nobranch
bat_a04	mapflag	nopenalty
bat_a04	mapflag	noecall
bat_a04	mapflag	bg_consume
