//===== rAthena Script =======================================
//    Extended Battleground
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
// Date:
//  10-10-2019
// By:
//  Easycore         (Zephyrus, original author)
//
// Contact:
//    - Facebook: https://www.facebook.com/EasycoreRo/
//    - rAthena:  https://rathena.org/board/profile/16425-easycore/
//    - Discord:  Easycore#3412
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
// BattleGround System - Flavius TeamDeathMatch
// ==============================================================================

// Battleground Queue Engine
// *********************************************************************
-	script	TD_BG_Queue	-1,{
	end;
	
OnInit:
	// Configuration
	.Num = 1;
	set .BG_Queue, bg_queue_create("Team DeathMatch",strnpcinfo(0)+"::OnJoinEvent",getvariableofnpc(.BG_level[.Num],"BG_Queue_Join"));
	.BG_Name$ = getvariableofnpc(.BG_Names$[.Num],"BG_Queue_Join");
	.BG_Arena$ = getvariableofnpc(.BG_Arenas$[.Num],"BG_Queue_Join");
	.BG_Min = getvariableofnpc(.BG_Min[.Num],"BG_Queue_Join");
	.BG_Max = getvariableofnpc(.BG_Max[.Num],"BG_Queue_Join");
	.TeamMode = getvariableofnpc(.TeamMode,"BG_Queue_Join");
	end;
	
	
OnJoinBG:
	if (getcharid(6) == .BG_Queue)
		end;
	if (getbattleflag("bg_queue_interface") == 0) {
		message strcharinfo(0),"Queue UI are currently disabled.";
		end;
	} else if (getbattleflag("bg_eAmod_mode") == 0) {
		message strcharinfo(0),"Battlegrounds are currently disabled.";
		end;
	} else if( agitcheck() || agitcheck2() ) {
		dispbottom "[Battleground is currently disabled for this moment due to War of Emperium.]";
		end;
	} else if (!getvariableofnpc(.Is_Enabled[.Num],"BG_Queue_Join")) {
		message strcharinfo(0),"This arena is currently disabled.";
		end;
	} else if (getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,0)) {
		dispbottom "[You are a Deserter. Time remaining: "+(getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,5)/1000)+" seconds]";
		end;
	}
	if (getcharid(6))
		bg_queue_leaveall();
	$@BG_User_Min = .BG_Min*2;
	bg_queue_join .BG_Queue;
	end;
OnDoLeave: // @leavebg
	bg_queue_leave .BG_Queue;
	end;

OnJoinEvent:
	if( $@BG_Status_TD == 2 )
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch( $@BG_Status_TD ) {
		case 0: // Idle - Ready to Start

			if( bg_queue_checkstart(.BG_Queue,.TeamMode,2,.BG_Min) ) {
				donpcevent .BG_Arena$+"::OnBuildTeams";

				// Fill Teams with Queue Members
				bg_queue2teams .BG_Queue,.BG_Min,.BG_Max,.TeamMode,$@BG_TD_Team1,$@BG_TD_Team2;

				set $@BG_Status_TD,1;
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- Begining";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");

				donpcevent .BG_Arena$+"::OnReady";
			} else {
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- " + (.BG_Min*2 - .@Queue_Count) + " more players to start.";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
		case 1: // Running - Others can join
			if( .@Queue_Count > 0 ) {
				bg_balance_teams .BG_Queue,.BG_Max,.TeamMode,$@BG_TD_Team1,$@BG_TD_Team2;
				set .@BG_Count1, bg_get_data($@BG_TD_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_TD_Team2,0);
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- B: " + .@BG_Count1 + "/" + .BG_Max + ", R: " + .@BG_Count2 + "/" + .BG_Max + " (Playing)";

				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
	}
	end;
}
// Battleground Engine
// *********************************************************************

-	script	Flavius_TD	FAKE_NPC,{
	end;

OnTeam1Quit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join"),0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_TD != 0)
		donpcevent "TD_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD")
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam1Die:
	if (($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD") || $@BG_Status_TD == 1) {
		if (killerrid == 0) end;
		set .Team1_Score, .Team1_Score - 1;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;

OnTeam2Quit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join"),0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_TD != 0)
		donpcevent "TD_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD")
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam2Die:
	if (($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD") || $@BG_Status_TD == 1) {
		if (killerrid == 0) end;
		set .Team2_Score, .Team2_Score - 1;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;

OnBuildTeams:
	set getd(.T1$),bg_create("bat_cc",88,139,0,"Flavius_TD::OnTeam1Quit","Flavius_TD::OnTeam1Die");
	set getd(.T2$),bg_create("bat_cc",96,139,1,"Flavius_TD::OnTeam2Quit","Flavius_TD::OnTeam2Die");
	end;

OnReady:
	if (!getbattleflag("bg_queue_interface") && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_TD")
		end;

	initnpctimer;
	set .Team1_Score, 50;
	set .Team2_Score, 50;
	bg_updatescore "bat_cc",.Team1_Score,.Team2_Score;
	sleep 2100;
	bg_warp getd(.T1$),"bat_cc",53,128;
	bg_warp getd(.T2$),"bat_cc",146,55;
	sleep 2100;
	bg_team_reveal getd(.T1$);
	bg_team_reveal getd(.T2$);
	donpcevent "#guitd_respawn::OnBGStart";
	donpcevent "#crotd_respawn::OnBGStart";
	donpceventall "OnEmblemTD";
	end;

OnValidateScore:
	if (getbattleflag("bg_queue_interface") && $@BG_Status_TD != 1)
		end;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_TD"))
		end;

	bg_updatescore "bat_cc",.Team1_Score,.Team2_Score;
	set .@Team1Count, bg_get_data(getd(.T1$), 0);
	set .@Team2Count, bg_get_data(getd(.T2$), 0);
	.@reward_win = 5;
	.@reward_lose = 3;
	// Team 1 Won
	if (.Team2_Score <= 0) {
		bg_reward getd(.T1$),7829,.@reward_win,0,0,"bat_cc",1,4,0;
		bg_reward getd(.T2$),7829,.@reward_lose,0,0,"bat_cc",1,4,2;
		mapannounce "bat_cc","The " + bg_get_data(getd(.T1$),2) + " army has won the Battle of Flavius TD!",1,bg_get_data(getd(.T1$),4);
	}
	// Team 2 Won
	else if (.Team1_Score <= 0) {
		bg_reward getd(.T1$),7829,.@reward_lose,0,0,"bat_cc",1,4,2;
		bg_reward getd(.T2$),7829,.@reward_win,0,0,"bat_cc",1,4,0;
		mapannounce "bat_cc","The " + bg_get_data(getd(.T2$),2) + " army has won the Battle of Flavius TD!",1,bg_get_data(getd(.T2$),4);
	}
	// All Team 2 Players quit
	else if (.@Team2Count == 0) {
		set .@Rate, 99 - .Team2_Score;
		bg_reward getd(.T1$),7829,.@reward_win,0,0,"bat_cc",1,4,0;
		mapannounce "bat_cc","The " + bg_get_data(getd(.T1$),2) + " army has won the Battle of Flavius TD!",1,bg_get_data(getd(.T1$),4);
	}
	// All Team 1 Players quit
	else if (.@Team1Count == 0) {
		set .@Rate, 99 - .Team1_Score;
		bg_reward getd(.T2$),7829,.@reward_win,0,0,"bat_cc",1,4,0;
		mapannounce "bat_cc","The " + bg_get_data(getd(.T2$),2) + " army has won the Battle of Flavius TD!",1,bg_get_data(getd(.T2$),4);
	}
	else end;
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnTimer600000:
	mapannounce "bat_cc","The Battle will ends in 5 minutes!!",1,0x808000;
	end;

OnTimer840000:
	mapannounce "bat_cc","The Battle will ends in 1 minute!!",1,0x808000;
	end;

OnTimer900000:
	.@reward_win = 5;
	.@reward_lose = 3;
	// Team 1 Won
	if (.Team1_Score > .Team2_Score) {
		bg_reward getd(.T1$),7829,.@reward_win,0,0,"bat_cc",1,4,0;
		bg_reward getd(.T2$),7829,.@reward_lose,0,0,"bat_cc",1,4,2;
		mapannounce "bat_cc","The " + bg_get_data(getd(.T1$),2) + " army has won the Battle of Flavius TD!",1,bg_get_data(getd(.T1$),4);
	}
	// Team 2 Won
	else if (.Team1_Score < .Team2_Score) {
		bg_reward getd(.T1$),7829,.@reward_lose,0,0,"bat_cc",1,4,2;
		bg_reward getd(.T2$),7829,.@reward_win,0,0,"bat_cc",1,4,0;
		mapannounce "bat_cc","The " + bg_get_data(getd(.T2$),2) + " army has won the Battle of Flavius TD!",1,bg_get_data(getd(.T2$),4);
	}
	else {
		bg_reward getd(.T1$),7829,.@reward_lose,0,0,"bat_cc",1,4,1;
		bg_reward getd(.T2$),7829,.@reward_lose,0,0,"bat_cc",1,4,1;
		mapannounce "bat_cc","The battle is over. This is a Tie...!",1,0x808000;
	}
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnMatchEnd:
	stopnpctimer;
	donpcevent "#guitd_respawn::OnBGStop";
	donpcevent "#crotd_respawn::OnBGStop";
	if (getbattleflag("bg_queue_interface"))
		set $@BG_Status_TD, 2;
	else
		set $@BG_Status, 2;
	set .Team1_Score, 50;
	set .Team2_Score, 50;
	sleep 5000;
	donpcevent "BG_Queue_Join::OnEndArena";
	bg_updatescore "bat_cc",50,50;
	end;
OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_TD_Team1";
		.T2$ = "$@BG_TD_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_cc,88,143,5	script	Therapist in battle#td1	4_F_SISTER,{
	getmapxy(.@map$,.@x,.@y, BL_PC);
	if (distance(.@x,.@y,88,143) > 6)
		end;
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
OnInit:
	setunittitle(getnpcid(0), "[Blue Team]");
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 100);
	end;
}

bat_cc,97,143,3	script	Therapist in battle#td2	4_F_SISTER,{
	getmapxy(.@map$,.@x,.@y, BL_PC);
	if (distance(.@x,.@y,97,143) > 6)
		end;
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
OnInit:
	setunittitle(getnpcid(0), "[Red Team]");
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 101);
	end;
}

// Battleground Respawn
// *********************************************************************

bat_cc,88,139,0	script	#guitd_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_cc",85,134,92,146,100,100;
	areawarp "bat_cc",85,134,92,146,"bat_cc",50,131,77,102;
	initnpctimer;
	end;
}

bat_cc,96,139,0	script	#crotd_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_cc",93,134,100,146,100,100;
	areawarp "bat_cc",93,134,100,146,"bat_cc",50,131,77,102;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************
bat_cc,88,146,4	script	Flag#TD_Blue	GUILD_FLAG,{
	end;
OnEmblemTD:
	if (!getbattleflag("bg_queue_interface"))
		.@t$ = "$@BG_Team1";
	else
		.@t$ = "$@BG_TD_Team1";

	flagemblembg getd(.@t$);
	end;
}
bat_cc,97,146,4	script	Flag#TD_Red	GUILD_FLAG,{
	end;
OnEmblemTD:
	if (!getbattleflag("bg_queue_interface"))
		.@t$ = "$@BG_Team2";
	else
		.@t$ = "$@BG_TD_Team2";

	flagemblembg getd(.@t$);
	end;
}

bat_cc,40,105,1	duplicate(Base Flag#bg)	Alpha Base#td_1	973
bat_cc,47,105,1	duplicate(Base Flag#bg)	Alpha Base#td_2	973

bat_cc,96,49,1	duplicate(Base Flag#bg)	Omega Base#td_1	974
bat_cc,103,49,1	duplicate(Base Flag#bg)	Omega Base#td_2	974

bat_cc,76,102,1	duplicate(Base Flag#bg)	Alpha Base#td_3	973
bat_cc,89,111,1	duplicate(Base Flag#bg)	Alpha Base#td_4	973
bat_cc,61,130,1	duplicate(Base Flag#bg)	Alpha Base#td_5	973
bat_cc,51,120,1	duplicate(Base Flag#bg)	Alpha Base#td_6	973

bat_cc,100,72,1	duplicate(Base Flag#bg)	Omega Base#td_3	974
bat_cc,123,81,1	duplicate(Base Flag#bg)	Omega Base#td_4	974
bat_cc,138,53,1	duplicate(Base Flag#bg)	Omega Base#td_5	974
bat_cc,148,63,1	duplicate(Base Flag#bg)	Omega Base#td_6	974

// MapFlags
// *********************************************************************

bat_cc	mapflag	battleground	2
bat_cc	mapflag	nomemo
bat_cc	mapflag	nosave	SavePoint
bat_cc	mapflag	noteleport
bat_cc	mapflag	nowarp
bat_cc	mapflag	nowarpto
bat_cc	mapflag	noreturn
bat_cc	mapflag	nobranch
bat_cc	mapflag	nopenalty
bat_cc	mapflag	bg_consume
