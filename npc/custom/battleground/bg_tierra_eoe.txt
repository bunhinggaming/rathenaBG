//===== rAthena Script =======================================
//    Extended Battleground
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
// Date:
//  10-10-2019
// By:
//  Easycore         (Zephyrus, original author)
//
// Contact:
//    - Facebook: https://www.facebook.com/EasycoreRo/
//    - rAthena:  https://rathena.org/board/profile/16425-easycore/
//    - Discord:  Easycore#3412
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 

// ==============================================================================
// BattleGround System - Tierra Valley Eye of Storm
// ==============================================================================

// Battleground Queue Engine
// *********************************************************************
-	script	EOS_BG_Queue	-1,{
	end;
	
OnInit:
	// Configuration
	.Num = 5;
	set .BG_Queue, bg_queue_create("Eye of Storm",strnpcinfo(0)+"::OnJoinEvent",getvariableofnpc(.BG_level[.Num],"BG_Queue_Join"));
	.BG_Name$ = getvariableofnpc(.BG_Names$[.Num],"BG_Queue_Join");
	.BG_Arena$ = getvariableofnpc(.BG_Arenas$[.Num],"BG_Queue_Join");
	.BG_Min = getvariableofnpc(.BG_Min[.Num],"BG_Queue_Join");
	.BG_Max = getvariableofnpc(.BG_Max[.Num],"BG_Queue_Join");
	.TeamMode = getvariableofnpc(.TeamMode,"BG_Queue_Join");
	end;
	
	
OnJoinBG:
	if (getcharid(6) == .BG_Queue)
		end;
	if (getbattleflag("bg_queue_interface") == 0) {
		message strcharinfo(0),"Queue UI are currently disabled.";
		end;
	} else if (getbattleflag("bg_eAmod_mode") == 0) {
		message strcharinfo(0),"Battlegrounds are currently disabled.";
		end;
	} else if( agitcheck() || agitcheck2() ) {
		dispbottom "[Battleground is currently disabled for this moment due to War of Emperium.]";
		end;
	} else if (!getvariableofnpc(.Is_Enabled[.Num],"BG_Queue_Join")) {
		message strcharinfo(0),"This arena is currently disabled.";
		end;
	} else if (getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,0)) {
		dispbottom "[You are a Deserter. Time remaining: "+(getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,5)/1000)+" seconds]";
		end;
	}
	if (getcharid(6))
		bg_queue_leaveall();
	$@BG_User_Min = .BG_Min*2;
	bg_queue_join .BG_Queue;
	end;
OnDoLeave: // @leavebg
	bg_queue_leave .BG_Queue;
	end;

OnJoinEvent:
	if( $@BG_Status_EOS == 2 )
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch( $@BG_Status_EOS ) {
		case 0: // Idle - Ready to Start

			if( bg_queue_checkstart(.BG_Queue,.TeamMode,2,.BG_Min) ) {
				donpcevent .BG_Arena$+"::OnBuildTeams";

				// Fill Teams with Queue Members
				bg_queue2teams .BG_Queue,.BG_Min,.BG_Max,.TeamMode,$@BG_EOS_Team1,$@BG_EOS_Team2;

				set $@BG_Status_EOS,1;
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- Begining";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");

				donpcevent .BG_Arena$+"::OnReady";
			} else {
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- " + (.BG_Min*2 - .@Queue_Count) + " more players to start.";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
		case 1: // Running - Others can join
			if( .@Queue_Count > 0 ) {
				bg_balance_teams .BG_Queue,.BG_Max,.TeamMode,$@BG_EOS_Team1,$@BG_EOS_Team2;
				set .@BG_Count1, bg_get_data($@BG_EOS_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_EOS_Team2,0);
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- B: " + .@BG_Count1 + "/" + .BG_Max + ", R: " + .@BG_Count2 + "/" + .BG_Max + " (Playing)";

				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
	}
	end;
}
// Battleground Engine
// *********************************************************************

-	script	Tierra_EOS	FAKE_NPC,{
	end;

OnTeam1Quit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join"),0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 0)
		donpcevent "EOS_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_EOS")
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam1Die:
	// Drop Flag
	if (($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_EOS") ||
	((getbattleflag("bg_queue_interface") && $@BG_Status_EOS == 1))
	&& getvariableofnpc(.Flag_Carrier,"Neutral_Flag") == getcharid(0)) {
		setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
		getmapxy .@m$, .@x, .@y, BL_PC;
		movenpc "Neutral_Flag", .@x, .@y;
		mapannounce "bat_a02",bg_get_data(getd(.T1$),2) + " have droped the Flag",1,0xFFFFFF;
		if (killerrid)
			bg_rankpoints "fame",0,1,killerrid;
		set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 0; // OnFloor
		set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
		initnpctimer "Neutral_Flag";
		enablenpc "Neutral_Flag";
	}
	end;

OnTeam2Quit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join"),0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 0)
		donpcevent "EOS_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_EOS")
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam2Die:
	// Drop Flag
	if (($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_EOS") ||
	(getbattleflag("bg_queue_interface") && $@BG_Status_EOS == 1)
	&& getvariableofnpc(.Flag_Carrier,"Neutral_Flag") == getcharid(0)) {
		setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
		getmapxy .@m$, .@x, .@y, BL_PC;
		movenpc "Neutral_Flag", .@x, .@y;
		mapannounce "bat_a02",bg_get_data(getd(.T2$),2) + " have droped the Flag",1,0xFFFFFF;
		if (killerrid)
			bg_rankpoints "fame",0,1,killerrid;
		set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 0; // OnFloor
		set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
		initnpctimer "Neutral_Flag";
		enablenpc "Neutral_Flag";
	}
	end;

OnBuildTeams:
	set getd(.T1$),bg_create("bat_a02",50,374,0,"Tierra_EOS::OnTeam1Quit","Tierra_EOS::OnTeam1Die");
	set getd(.T2$),bg_create("bat_a02",42,16,1,"Tierra_EOS::OnTeam2Quit","Tierra_EOS::OnTeam2Die");
	end;

OnReady:
	if (!getbattleflag("bg_queue_interface") && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS")
		end;

	// BG Variables
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 2100;
	bg_warp getd(.T1$),"bat_a02",353,344;
	bg_warp getd(.T2$),"bat_a02",353,52;
	sleep 2100;
	// Respawn NPC's
	donpcevent "#guieoe_respawn::OnBGStart";
	donpcevent "#croeoe_respawn::OnBGStart";
	// Start Match!!
	donpcevent "North_Base::OnBase";
	donpcevent "South_Base::OnBase";
	donpcevent "Neutral_Flag::OnBase";
	donpcevent "::OnStartStorm";
	mapannounce "bat_a02","The Battle of Tierra Valley - Eye of Storm has begun",1,0x4169E1;
	end;

OnFlash:
	if (getvariableofnpc(.Flag_Carrier,"Neutral_Flag") == getcharid(0) && (($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_EOS") || $@BG_Status_EOS == 1)) {
		getmapxy .@m$, .@x, .@y, BL_PC;
		viewpointmap "bat_a02",1,.@x,.@y,3,0xFFFFFF;
		specialeffect 73;
		emotion ET_HELP,getcharid(3);
		addtimer 2100, "Tierra_EOS::OnFlash";
		percentheal -5,-5;
	}
	end;

OnTeam1Flag:
	viewpointmap "bat_a02",2,0,0,3,0xFFFFFF;
	set .@North, getvariableofnpc(.Owner,"North_Base");
	set .@South, getvariableofnpc(.Owner,"South_Base");

	if (.@North == .@South) {
		set .Team1_Score, .Team1_Score + 5;
		mapannounce "bat_a02",bg_get_data(getd(.T1$),2) + " captured the Flag [+5 points]",1,0x0000FF;
	} else {
		set .Team1_Score, .Team1_Score + 3;
		mapannounce "bat_a02",bg_get_data(getd(.T1$),2) + " captured the Flag [+3 points]",1,0x0000FF;
	}

	donpcevent "Tierra_EOS::OnValidateScore";
	donpcevent "Neutral_Flag::OnBase";
	end;

OnTeam2Flag:
	viewpointmap "bat_a02",2,0,0,3,0xFFFFFF;
	set .@North, getvariableofnpc(.Owner,"North_Base");
	set .@South, getvariableofnpc(.Owner,"South_Base");
	
	if (.@North == .@South) {
		set .Team2_Score, .Team2_Score + 5;
		mapannounce "bat_a02",bg_get_data(getd(.T2$),2) + " captured the Flag [+5 points]",1,0xFF0000;
	} else {
		set .Team2_Score, .Team2_Score + 3;
		mapannounce "bat_a02",bg_get_data(getd(.T2$),2) + " captured the Flag [+3 points]",1,0xFF0000;
	}

	donpcevent "Tierra_EOS::OnValidateScore";
	donpcevent "Neutral_Flag::OnBase";
	end;

OnNorthScore:
	if (set(.@North, getvariableofnpc(.Owner,"North_Base")) == 0)
		end; // No Owner
	set .@South, getvariableofnpc(.Owner,"South_Base");
	// Double Domination
	if (.@North == .@South) {
		if (.@North == getd(.T1$))
			set .Team1_Score, .Team1_Score + 2;
		else
			set .Team2_Score, .Team2_Score + 2;
	} else if (.@North == getd(.T1$))
		set .Team1_Score, .Team1_Score + 1;
	else
		set .Team2_Score, .Team2_Score + 1;

	donpcevent "Tierra_EOS::OnValidateScore";
	end;
	
OnSouthScore:
	if (set(.@South, getvariableofnpc(.Owner,"South_Base")) == 0)
		end; // No Owner
	set .@North, getvariableofnpc(.Owner,"North_Base");
	// Double Domination
	if (.@North == .@South) {
		if (.@South == getd(.T1$))
			set .Team1_Score, .Team1_Score + 2;
		else
			set .Team2_Score, .Team2_Score + 2;
	} else if (.@South == getd(.T1$))
		set .Team1_Score, .Team1_Score + 1;
	else
		set .Team2_Score, .Team2_Score + 1;

	donpcevent "Tierra_EOS::OnValidateScore";
	end;

OnValidateScore:
	if (.Team1_Score > 99)
		set .Team1_Score, 99;
	if (.Team2_Score > 99)
		set .Team2_Score, 99;

	bg_updatescore "bat_a02",.Team1_Score,.Team2_Score; // Update Visual Score

	if (.Team1_Score < 99 && .Team2_Score < 99)
		end; // No winners
OnMatchEnd:

	donpcevent "Neutral_Flag::OnDisable";
	stopnpctimer "North_Base";
	stopnpctimer "South_Base";
	donpcevent "#guieoe_respawn::OnBGStop";
	donpcevent "#croeoe_respawn::OnBGStop";
	if (getbattleflag("bg_queue_interface"))
		set $@BG_Status_EOS, 2;
	else
		set $@BG_Status, 2;
	// =======================================================
	// Team Rewards
	// =======================================================
	.@reward_win = 5;
	.@reward_lose = 3;

	// Tie
	if (.Team1_Score >= 99 && .Team2_Score >= 99) {
		bg_reward getd(.T1$),7828,.@reward_lose,0,0,"BG_EoE",1,0,1;
		bg_reward getd(.T2$),7828,.@reward_lose,0,0,"BG_EoE",1,0,1;
		mapannounce "bat_a02","The battle is over. This is a Tie...!",1,0x4169E1;
	}
	// Team 1 Won
	else if (.Team1_Score >= 99) {
		bg_reward getd(.T1$),7828,.@reward_win,0,0,"BG_EoE",1,0,0;
		bg_reward getd(.T2$),7828,.@reward_lose,0,0,"BG_EoE",1,0,2;
		mapannounce "bat_a02","The " + bg_get_data(getd(.T1$),2) + " has won the Battle of Tierra EoS!",1,bg_get_data(getd(.T1$),4);
	}
	// Team 2 Won
	else if (.Team2_Score >= 99) {
		bg_reward getd(.T1$),7828,.@reward_lose,0,0,"BG_EoE",1,0,2;
		bg_reward getd(.T2$),7828,.@reward_win,0,0,"BG_EoE",1,0,0;
		mapannounce "bat_a02","The " + bg_get_data(getd(.T2$),2) + " has won the Battle of Tierra EoS!",1,bg_get_data(getd(.T2$),4);
	}
	// =======================================================
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 5000;
	donpcevent "BG_Queue_Join::OnEndArena";
	bg_updatescore "bat_a02",0,0;
	donpcevent "GEoSNBW::OnDisable";
	//donpcevent "GEoSCBW::OnDisable";
	donpcevent "GEoSSBW::OnDisable";
	donpcevent "CEoSNBW::OnDisable";
	//donpcevent "CEoSCBW::OnDisable";
	donpcevent "CEoSSBW::OnDisable";
	end;

OnBreak:
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;

	if (getcharid(4) == getd(.T1$))
		mapannounce "bat_a02","Netraul Flag Taken by " + bg_get_data(getd(.T1$),2),1,bg_get_data(getd(.T1$),4);
	else if (getcharid(4) == getd(.T2$))
		mapannounce "bat_a02","Netraul Flag Taken by " + bg_get_data(getd(.T2$),2),1,bg_get_data(getd(.T2$),4);
	else end;

	set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 1;
	set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), getcharid(0);

	sc_end SC_HIDING;
	sc_end SC_CLOAKING;
	sc_end SC_CHASEWALK;

	// Renewal invisibility
	sc_end SC_CLOAKINGEXCEED;
	sc_end SC_CAMOUFLAGE;
	sc_end SC__INVISIBILITY;

	setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,true);

	stopnpctimer "Neutral_Flag";
	disablenpc "Neutral_Flag";

	addtimer 2100, "Tierra_EOS::OnFlash";
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_EOS_Team1";
		.T2$ = "$@BG_EOS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

// Battleground Flag
// *********************************************************************

bat_a02,273,204,0	script	Neutral Flag::Neutral_Flag	1911,1,1,{
	end;

OnTouch:
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 1)
		end;
	if (Hp < 1 || .Flag_Status)
		end;

	if (getcharid(4) == getd(.T1$))
		mapannounce "bat_a02","Netraul Flag Taken by " + bg_get_data(getd(.T1$),2),1,bg_get_data(getd(.T1$),4);
	else if (getcharid(4) == getd(.T2$))
		mapannounce "bat_a02","Netraul Flag Taken by " + bg_get_data(getd(.T2$),2),1,bg_get_data(getd(.T2$),4);
	else end;

	set .Flag_Status, 1;
	set .Flag_Carrier, getcharid(0);
	bg_rankpoints "fame",0,1;

	sc_end SC_HIDING;
	sc_end SC_CLOAKING;
	sc_end SC_CHASEWALK;

	// Renewal invisibility
	sc_end SC_CLOAKINGEXCEED;
	sc_end SC_CAMOUFLAGE;
	sc_end SC__INVISIBILITY;

	setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,true);

	disablenpc "Neutral_Flag";
	addtimer 2100, "Tierra_EOS::OnFlash";
	stopnpctimer;
	end;

OnTimer2000:
	stopnpctimer;
	if (.Flag_Status == 0 && (($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Tierra_EOS") || $@BG_Status_EOS == 1)) {
		getmapxy .@m$, .@x, .@y, BL_NPC;
		viewpointmap "bat_a02",1,.@x,.@y,3,0xFFFFFF;
		specialeffect 223;
		initnpctimer;
	}
	end;

OnDisable:
	movenpc "Neutral_Flag",273,204;
	set .Flag_Status, 0;
	stopnpctimer;
	disablenpc "Neutral_Flag";
	killmonster "bat_a02","Tierra_EOS::OnBreak";
	end;

OnBase:
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 1)
		end;

	movenpc "Neutral_Flag",273,204;
	set .Flag_Status, 0;
	set .Flag_Carrier, 0;
	initnpctimer;
	disablenpc "Neutral_Flag";
	mapannounce "bat_a02","The Neutral Flag have been set!!",1,0xFFFFFF;
	bg_monster 0,"bat_a02",273,204,"Neutral Flag",1911,"Tierra_EOS::OnBreak";
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_EOS_Team1";
		.T2$ = "$@BG_EOS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

// Battleground Bases
// *********************************************************************

bat_a02,173,345,0	script	North Base::North_Base	1911,3,3,{
	end;

OnTouch:
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 1)
		end;
	if (.Owner == 0 || .Owner != getcharid(4) || getvariableofnpc(.Flag_Carrier,"Neutral_Flag") != getcharid(0))
		end;

	bg_rankpoints "eos_flags",1,$@eos_flag;
	set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
	setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
	if (.Owner == $@BG_Team1)
		donpcevent "Tierra_EOS::OnTeam1Flag";
	else
		donpcevent "Tierra_EOS::OnTeam2Flag";
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	set .Tick, 0;
	setnpcdisplay "North_Base","North Base",1911;
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 1)
		end;

	set .@Team1Count, bg_getareausers(getd(.T1$),"bat_a02",136,329,186,361);
	set .@Team2Count, bg_getareausers(getd(.T2$),"bat_a02",136,329,186,361);
	set .Balance, .Balance + set(.@Balance, .@Team1Count - .@Team2Count);

	if (.Balance < -50)
		set .Balance, -50;
	else if (.Balance > 50)
		set .Balance, 50;

	if (.Owner == 0) {
		if (.Balance == 50) {
			set .Balance, 50;
			set .Owner, getd(.T1$); // Team 1
			setnpcdisplay "North_Base",bg_get_data(getd(.T1$),2) + " Base",1912;
			mapannounce "bat_a02","North Base captured by " + bg_get_data(getd(.T1$),2),1,bg_get_data(getd(.T1$),4);
			bg_rankpoints_area getd(.T1$),"bat_a02",136,329,186,361,"eos_bases",1,$@eos_bases;
			donpcevent "GEoSNBW::OnEnable";
		} else if (.Balance == -50) {
			set .Balance, -50;
			set .Owner, getd(.T2$); // Team 2
			setnpcdisplay "North_Base",bg_get_data(getd(.T2$),2) + " Base",1913;
			mapannounce "bat_a02","North Base captured by " + bg_get_data(getd(.T2$),2),1,bg_get_data(getd(.T2$),4);
			bg_rankpoints_area getd(.T2$),"bat_a02",136,329,186,361,"eos_bases",1,$@eos_bases;
			donpcevent "CEoSNBW::OnEnable";
		}
	} else if (.Owner == getd(.T1$)) {
		if (.Balance <= 0) { // Team 1 lost Control
			set .Owner, 0;
			setnpcdisplay "North_Base","North Base",1911;
			mapannounce "bat_a02",bg_get_data(getd(.T1$),2) + " lost control of the North Base",1,bg_get_data(getd(.T1$),4);
			donpcevent "GEoSNBW::OnDisable";
		}
	} else if (.Owner == getd(.T2$)) {
		if (.Balance >= 0) { // Team 2 lost Control
			set .Owner, 0;
			setnpcdisplay "North_Base","North Base",1911;
			mapannounce "bat_a02",bg_get_data(getd(.T2$),2) + " lost control of the North Base",1,bg_get_data(getd(.T2$),4);
			donpcevent "CEoSNBW::OnDisable";
		}
	}

	if (.@Balance > 0)
		specialeffect 236;
	else if (.@Balance < 0)
		specialeffect 225;
	else if (.Owner == getd(.T1$))
		specialeffect 236;
	else if (.Owner == getd(.T2$))
		specialeffect 225;
	else
		specialeffect 223;
	
	if (.Owner == 0)
		viewpointmap "bat_a02",1,173,345,1,0xFFFFFF;
	else if (.Owner == getd(.T1$))
		viewpointmap "bat_a02",1,173,345,1,bg_get_data(getd(.T1$),4);
	else if (.Owner == getd(.T2$))
		viewpointmap "bat_a02",1,173,345,1,bg_get_data(getd(.T2$),4);

	if (.Owner == 0)
		set .Tick, 0;
	else if (set(.Tick, .Tick + 1) == 7) {
		donpcevent "Tierra_EOS::OnNorthScore";
		set .Tick, 0;
	}

	initnpctimer;
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_EOS_Team1";
		.T2$ = "$@BG_EOS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

bat_a02,164,50,0	script	South Base::South_Base	1911,3,3,{
	end;

OnTouch:
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 1)
		end;
	if (.Owner == 0 || .Owner != getcharid(4) || getvariableofnpc(.Flag_Carrier,"Neutral_Flag") != getcharid(0))
		end;

	bg_rankpoints "eos_flags",1,$@eos_flags;
	set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
	setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
	if (.Owner == $@BG_Team1)
		donpcevent "Tierra_EOS::OnTeam1Flag";
	else
		donpcevent "Tierra_EOS::OnTeam2Flag";
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	set .Tick, 0;
	setnpcdisplay "South_Base","South Base",1911;
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 1)
		end;

	set .@Team1Count, bg_getareausers(getd(.T1$),"bat_a02",129,34,175,65);
	set .@Team2Count, bg_getareausers(getd(.T2$),"bat_a02",129,34,175,65);
	set .Balance, .Balance + set(.@Balance, .@Team1Count - .@Team2Count);

	if (.Balance < -50)
		set .Balance, -50;
	else if (.Balance > 50)
		set .Balance, 50;

	if (.Owner == 0) {
		if (.Balance == 50) {
			set .Balance, 50;
			set .Owner, getd(.T1$); // Team 1
			setnpcdisplay "South_Base",bg_get_data(getd(.T1$),2) + " Base",1912;
			mapannounce "bat_a02","South Base captured by " + bg_get_data(getd(.T1$),2),1,bg_get_data(getd(.T1$),4);
			bg_rankpoints_area getd(.T1$),"bat_a02",129,34,175,65,"eos_bases",1,$@eos_bases;
			donpcevent "GEoSSBW::OnEnable";
		} else if (.Balance == -50) {
			set .Balance, -50;
			set .Owner, getd(.T2$); // Team 2
			setnpcdisplay "South_Base",bg_get_data(getd(.T2$),2) + " Base",1913;
			mapannounce "bat_a02","South Base captured by " + bg_get_data(getd(.T2$),2),1,bg_get_data(getd(.T2$),4);
			bg_rankpoints_area getd(.T2$),"bat_a02",129,34,175,65,"eos_bases",1,$@eos_bases;
			donpcevent "CEoSSBW::OnEnable";
		}
	} else if (.Owner == getd(.T1$)) {
		if (.Balance <= 0) { // Team 1 lost Control
			set .Owner, 0;
			setnpcdisplay "South_Base","North Base",1911;
			mapannounce "bat_a02",bg_get_data(getd(.T1$),2) + " lost control of the South Base",1,bg_get_data(getd(.T1$),4);
			donpcevent "GEoSSBW::OnDisable";
		}
	} else if (.Owner == getd(.T2$)) {
		if (.Balance >= 0) { // Team 2 lost Control
			set .Owner, 0;
			setnpcdisplay "South_Base","North Base",1911;
			mapannounce "bat_a02",bg_get_data(getd(.T2$),2) + " lost control of the South Base",1,bg_get_data(getd(.T2$),4);
			donpcevent "CEoSSBW::OnDisable";
		}
	}

	if (.@Balance > 0)
		specialeffect 236;
	else if (.@Balance < 0)
		specialeffect 225;
	else if (.Owner == getd(.T1$))
		specialeffect 236;
	else if (.Owner == getd(.T2$))
		specialeffect 225;
	else
		specialeffect 223;
	
	if (.Owner == 0)
		viewpointmap "bat_a02",1,164,50,2,0xFFFFFF;
	else if (.Owner == getd(.T1$))
		viewpointmap "bat_a02",1,164,50,2,bg_get_data(getd(.T1$),4);
	else if (.Owner == getd(.T2$))
		viewpointmap "bat_a02",1,164,50,2,bg_get_data(getd(.T2$),4);

	if (.Owner == 0)
		set .Tick, 0;
	else if (set(.Tick, .Tick + 1) == 7) {
		donpcevent "Tierra_EOS::OnSouthScore";
		set .Tick, 0;
	}

	initnpctimer;
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_EOS_Team1";
		.T2$ = "$@BG_EOS_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_a02,53,377,3	script	Therapist in battle#eoe2	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

bat_a02,45,18,3	script	Therapist in battle#eoe1	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

// Battleground Respawn
// *********************************************************************

bat_a02,46,377,0	script	North Base Warp::GEoSNBW	1_SHADOW_NPC,{
	end;

	OnEnable:
		waitingroom "Join To Warp",20;
		end;

	OnDisable:
		delwaitingroom;
		end;

	OnWarp:
		warpwaitingpc "bat_a02",115,346,20;
		end;
}

bat_a02,53,370,0	script	South Base Warp::GEoSSBW	1_SHADOW_NPC,{
	end;

	OnEnable:
		waitingroom "Join To Warp",20;
		end;

	OnDisable:
		delwaitingroom;
		end;

	OnWarp:
		warpwaitingpc "bat_a02",106,48,20;
		end;
}

// *********************************************************************

bat_a02,38,19,0	script	North Base Warp::CEoSNBW	1_SHADOW_NPC,{
	end;

	OnEnable:
		waitingroom "Join To Warp",20;
		end;

	OnDisable:
		delwaitingroom;
		end;

	OnWarp:
		warpwaitingpc "bat_a02",115,346,20;
		end;
}

bat_a02,45,12,0	script	South Base Warp::CEoSSBW	1_SHADOW_NPC,{
	end;

	OnEnable:
		waitingroom "Join To Warp",20;
		end;

	OnDisable:
		delwaitingroom;
		end;

	OnWarp:
		warpwaitingpc "bat_a02",106,48,20;
		end;
}

// *********************************************************************

bat_a02,50,374,0	script	#guieoe_respawn	HIDDEN_WARP_NPC,{
	end;

	OnBGStart:
		initnpctimer;
		end;

	OnBGStop:
		stopnpctimer;
		end;

	OnTimer24000:
		specialeffect 83;
		end;

	OnTimer25000:
		areapercentheal "bat_a02",46,370,54,378,100,100;
		donpcevent "GEoSNBW::OnWarp";
		donpcevent "GEoSSBW::OnWarp";
		areawarp "bat_a02",46,370,54,378,"bat_a02",353,344;
		initnpctimer;
		end;
}

bat_a02,42,16,0	script	#croeoe_respawn	HIDDEN_WARP_NPC,{
	end;

	OnBGStart:
		initnpctimer;
		end;

	OnBGStop:
		stopnpctimer;
		end;

	OnTimer24000:
		specialeffect 83;
		end;

	OnTimer25000:
		areapercentheal "bat_a02",38,12,47,21,100,100;
		donpcevent "CEoSNBW::OnWarp";
		donpcevent "CEoSSBW::OnWarp";
		areawarp "bat_a02",38,12,47,21,"bat_a02",353,52;
		initnpctimer;
		end;
}

// MapFlags
// *********************************************************************

bat_a02	mapflag	battleground	2
bat_a02	mapflag	nomemo
bat_a02	mapflag	nosave	SavePoint
bat_a02	mapflag	noteleport
bat_a02	mapflag	nowarp
bat_a02	mapflag	nowarpto
bat_a02	mapflag	noreturn
bat_a02	mapflag	nobranch
bat_a02	mapflag	nopenalty
bat_a02	mapflag	noecall
bat_a02	mapflag	bg_consume

// Eye of the Storm Effects
// *********************************************************************

bat_a02,269,189,0	script	#stormef1	HIDDEN_WARP_NPC,{
	end;

OnStartStorm:
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Tierra_EOS"))
		end;
	if (getbattleflag("bg_queue_interface") && $@BG_Status_EOS != 1)
		end;

	sleep (rand(15,40) * 1000);
	specialeffect 622;
	specialeffect 537;

	callsub OnStartStorm;
	end;
}

bat_a02,284,189,0	duplicate(#stormef1)	#stormef2	HIDDEN_WARP_NPC
bat_a02,267,204,0	duplicate(#stormef1)	#stormef3	HIDDEN_WARP_NPC
bat_a02,278,210,0	duplicate(#stormef1)	#stormef4	HIDDEN_WARP_NPC
bat_a02,262,210,0	duplicate(#stormef1)	#stormef5	HIDDEN_WARP_NPC
bat_a02,256,201,0	duplicate(#stormef1)	#stormef6	HIDDEN_WARP_NPC
bat_a02,284,218,0	duplicate(#stormef1)	#stormef7	HIDDEN_WARP_NPC
bat_a02,263,220,0	duplicate(#stormef1)	#stormef8	HIDDEN_WARP_NPC
bat_a02,289,207,0	duplicate(#stormef1)	#stormef9	HIDDEN_WARP_NPC
bat_a02,279,182,0	duplicate(#stormef1)	#stormef10	HIDDEN_WARP_NPC
bat_a02,272,229,0	duplicate(#stormef1)	#stormef11	HIDDEN_WARP_NPC
