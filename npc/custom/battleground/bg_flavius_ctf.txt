//===== rAthena Script =======================================
//    Extended Battleground
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
// Date:
//  10-10-2019
// By:
//  Easycore         (Zephyrus, original author)
//
// Contact:
//    - Facebook: https://www.facebook.com/EasycoreRo/
//    - rAthena:  https://rathena.org/board/profile/16425-easycore/
//    - Discord:  Easycore#3412
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 

// Battleground Queue Engine
// *********************************************************************
-	script	CTF_BG_Queue	-1,{
	end;
	
OnInit:
	// Configuration
	.Num = 0;
	set .BG_Queue, bg_queue_create("Capture The Flag",strnpcinfo(0)+"::OnJoinEvent",getvariableofnpc(.BG_level[.Num],"BG_Queue_Join"));
	.BG_Name$ = getvariableofnpc(.BG_Names$[.Num],"BG_Queue_Join");
	.BG_Arena$ = getvariableofnpc(.BG_Arenas$[.Num],"BG_Queue_Join");
	.BG_Min = getvariableofnpc(.BG_Min[.Num],"BG_Queue_Join");
	.BG_Max = getvariableofnpc(.BG_Max[.Num],"BG_Queue_Join");
	.TeamMode = getvariableofnpc(.TeamMode,"BG_Queue_Join");
	end;
	
	
OnJoinBG:
	if (getcharid(6) == .BG_Queue)
		end;
	if (getbattleflag("bg_queue_interface") == 0) {
		message strcharinfo(0),"Queue UI are currently disabled.";
		end;
	} else if (getbattleflag("bg_eAmod_mode") == 0) {
		message strcharinfo(0),"Battlegrounds are currently disabled.";
		end;
	} else if( agitcheck() || agitcheck2() ) {
		dispbottom "[Battleground is currently disabled for this moment due to War of Emperium.]";
		end;
	} else if (!getvariableofnpc(.Is_Enabled[.Num],"BG_Queue_Join")) {
		message strcharinfo(0),"This arena is currently disabled.";
		end;
	} else if (getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,0)) {
		dispbottom "[You are a Deserter. Time remaining: "+(getstatus(SC_ENTRY_QUEUE_APPLY_DELAY,5)/1000)+" seconds]";
		end;
	}
	if (getcharid(6))
		bg_queue_leaveall();
	$@BG_User_Min = .BG_Min*2;
	bg_queue_join .BG_Queue;
	end;
OnDoLeave: // @leavebg
	bg_queue_leave .BG_Queue;
	end;

OnJoinEvent:
	if( $@BG_Status_CTF == 2 )
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch( $@BG_Status_CTF ) {
		case 0: // Idle - Ready to Start

			if( bg_queue_checkstart(.BG_Queue,.TeamMode,2,.BG_Min) ) {
				donpcevent .BG_Arena$+"::OnBuildTeams";

				// Fill Teams with Queue Members
				bg_queue2teams .BG_Queue,.BG_Min,.BG_Max,.TeamMode,$@BG_CTF_Team1,$@BG_CTF_Team2;

				set $@BG_Status_CTF,1;
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- Begining";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");

				donpcevent .BG_Arena$+"::OnReady";
			} else {
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- " + (.BG_Min*2 - .@Queue_Count) + " more players to start.";
				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
		case 1: // Running - Others can join
			if( .@Queue_Count > 0 ) {
				bg_balance_teams .BG_Queue,.BG_Max,.TeamMode,$@BG_CTF_Team1,$@BG_CTF_Team2;
				set .@BG_Count1, bg_get_data($@BG_CTF_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_CTF_Team2,0);
				set .@msg$, "Battleground -- " + .BG_Name$ + " -- B: " + .@BG_Count1 + "/" + .BG_Max + ", R: " + .@BG_Count2 + "/" + .BG_Max + " (Playing)";

				bgannounce .@msg$,getvariableofnpc(.BG_Color$[.Num],"BG_Queue_Join");
			}
			break;
	}
	end;
}
// ==============================================================================
// BattleGround System - Flavius Capture the Flag
// ==============================================================================

// Battleground Flags
// *********************************************************************

bat_b02,328,150,0	script	Alpha Crystal::Team1_Flag	1914,1,1,{
	end;

OnTouch:
	if (getbattleflag("bg_queue_interface") && ($@BG_Status_CTF != 1 || Hp < 1))
		end;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_CTF" || Hp < 1))
		end;

	// Flag Captured
	if ((getcharid(4) == $@BG_Team2 || getcharid(4) == $@BG_CTF_Team2) && .Flag_Status < 2) {
		set .Flag_Status, 2; // Taken
		set .Flag_Carrier, getcharid(0);

		sc_end SC_HIDING;
		sc_end SC_CLOAKING;
		sc_end SC_CHASEWALK;
		// Renewal invisibility
		sc_end SC_CLOAKINGEXCEED;
		sc_end SC_CAMOUFLAGE;
		sc_end SC__INVISIBILITY;

		setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,true);

		if ($@BG_Team1)
			mapannounce "bat_b02","Alpha Crystal Taken by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_Team1,4);
		else
			mapannounce "bat_b02","Alpha Crystal Taken by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_CTF_Team1,4);
		bg_rankpoints "ctf_taken",1,$@ctf_taken;
		disablenpc "Team1_Flag";
		addtimer 2100, "Flavius_CTF::OnAlphaFlash";
		stopnpctimer;
	} else if (getcharid(4) == $@BG_Team1 || getcharid(4) == $@BG_CTF_Team1) {
		if (.Flag_Status == 0 && getvariableofnpc(.Flag_Carrier,"Team2_Flag") == getcharid(0)) {
			set getvariableofnpc(.Flag_Carrier,"Team2_Flag"),0;
			set .Flag_Carrier, 0;
			if ($@BG_Team2)
				mapannounce "bat_b02","Omega Crystal Captured by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_Team2,4);
			else
				mapannounce "bat_b02","Omega Crystal Captured by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_CTF_Team2,4);
			bg_rankpoints "ctf_captured",1,$@ctf_captured;
			setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
			stopnpctimer;
			donpcevent "Flavius_CTF::OnTeam1Score";
		} else if (.Flag_Status == 1) {
			if ($@BG_Team1)
				mapannounce "bat_b02","Alpha Crystal Returned by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_Team1,4);
			else
				mapannounce "bat_b02","Alpha Crystal Returned by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_CTF_Team1,4);
			bg_rankpoints "fame",0,1;
			disablenpc "Team1_Flag";
			sleep 2100;
			movenpc "Team1_Flag",328,150; // Back to Base
			set .Flag_Status, 0;
			initnpctimer;
			enablenpc "Team1_Flag";
		}
	}
	end;

OnTimer2000:
	stopnpctimer;
	if (.Flag_Status < 2) {
		getmapxy .@m$, .@x, .@y, BL_NPC;
		if ($@BG_Team1)
			viewpointmap "bat_b02",1, .@x, .@y, 1, bg_get_data($@BG_Team1,4);
		else
			viewpointmap "bat_b02",1, .@x, .@y, 1, bg_get_data($@BG_CTF_Team1,4);
		specialeffect 223;
		initnpctimer;
	}
	end;

OnBase:
	movenpc "Team1_Flag",328,150;
	set .Flag_Status, 0;
	set .Flag_Carrier, 0;
	initnpctimer;
	enablenpc "Team1_Flag";
	end;

OnInit:
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 100);
	end;
}

bat_b02,62,150,0	script	Omega Crystal::Team2_Flag	1915,1,1,{
	end;

OnTouch:
	if (getbattleflag("bg_queue_interface") && ($@BG_Status_CTF != 1 || Hp < 1))
		end;
	if (!getbattleflag("bg_queue_interface") && ($@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_CTF" || Hp < 1))
		end;

	// Flag Captured
	if ((getcharid(4) == $@BG_Team1 ||getcharid(4) == $@BG_CTF_Team1) && .Flag_Status < 2) {
		set .Flag_Status, 2; // Taken
		set .Flag_Carrier, getcharid(0);

		sc_end SC_HIDING;
		sc_end SC_CLOAKING;
		sc_end SC_CHASEWALK;
		// Renewal invisibility
		sc_end SC_CLOAKINGEXCEED;
		sc_end SC_CAMOUFLAGE;
		sc_end SC__INVISIBILITY;

		setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,true);

		if ($@BG_Team2)
			mapannounce "bat_b02","Omega Crystal Taken by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_Team2,4);
		else
			mapannounce "bat_b02","Omega Crystal Taken by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_CTF_Team2,4);
		bg_rankpoints "ctf_taken",1,$@ctf_taken;
		disablenpc "Team2_Flag";
		addtimer 2100, "Flavius_CTF::OnOmegaFlash";
		stopnpctimer;
	} else if (getcharid(4) == $@BG_Team2 || getcharid(4) == $@BG_CTF_Team2) {
		if (.Flag_Status == 0 && getvariableofnpc(.Flag_Carrier,"Team1_Flag") == getcharid(0)) {
			set getvariableofnpc(.Flag_Carrier,"Team1_Flag"),0;
			set .Flag_Carrier, 0;
			if ($@BG_Team1)
				mapannounce "bat_b02","Alpha Crystal Captured by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_Team1,4);
			else
				mapannounce "bat_b02","Alpha Crystal Captured by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_CTF_Team1,4);
			bg_rankpoints "ctf_captured",1,$@ctf_captured;
			setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
			stopnpctimer;
			donpcevent "Flavius_CTF::OnTeam2Score";
		} else if (.Flag_Status == 1) {
			if ($@BG_Team2)
				mapannounce "bat_b02","Omega Crystal Returned by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_Team2,4);
			else
				mapannounce "bat_b02","Omega Crystal Returned by [ " + strcharinfo(0) + " ]!!",1,bg_get_data($@BG_CTF_Team2,4);
			bg_rankpoints "fame",0,1;
			disablenpc "Team2_Flag";
			sleep 2100;
			movenpc "Team2_Flag",62,150; // Back to Base
			set .Flag_Status, 0;
			initnpctimer;
			enablenpc "Team2_Flag";
		}
	}
	end;

OnTimer2000:
	stopnpctimer;
	if (.Flag_Status < 2) {
		getmapxy .@m$, .@x, .@y, BL_NPC;
		if ($@BG_Team2)
			viewpointmap "bat_b02",1, .@x, .@y, 2, bg_get_data($@BG_Team2,4);
		else
			viewpointmap "bat_b02",1, .@x, .@y, 2, bg_get_data($@BG_CTF_Team2,4);
		specialeffect 223;
		initnpctimer;
	}
	end;

OnBase:
	movenpc "Team2_Flag",62,150;
	set .Flag_Status, 0;
	set .Flag_Carrier, 0;
	initnpctimer;
	enablenpc "Team2_Flag";
	end;

OnInit:
	setunitdata(getnpcid(0), UNPC_GROUP_ID, 101);
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_CTF	FAKE_NPC,{
	end;

OnAlphaFlash:
	if (getvariableofnpc(.Flag_Carrier,"Team1_Flag") == getcharid(0) && 
	(($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_CTF") ||
	(getbattleflag("bg_queue_interface") && $@BG_Status_CTF == 1)))
	{
		getmapxy .@m$, .@x, .@y, BL_PC;
		if ($@BG_Team1)
			viewpointmap "bat_b02",1, .@x, .@y, 1, bg_get_data($@BG_Team1,4);
		else
			viewpointmap "bat_b02",1, .@x, .@y, 1, bg_get_data($@BG_CTF_Team1,4);
		specialeffect 73;
		emotion ET_HELP,getcharid(3);
		addtimer 2100, "Flavius_CTF::OnAlphaFlash";
		percentheal -5,-5;
	}
	end;

OnOmegaFlash:
	if (getvariableofnpc(.Flag_Carrier,"Team2_Flag") == getcharid(0) && 
	(($@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_CTF") ||
	(getbattleflag("bg_queue_interface") && $@BG_Status_CTF == 1)))
	{
		getmapxy .@m$, .@x, .@y, BL_PC;
		if ($@BG_Team2)
			viewpointmap "bat_b02",1, .@x, .@y, 2, bg_get_data($@BG_Team2,4);
		else
			viewpointmap "bat_b02",1, .@x, .@y, 2, bg_get_data($@BG_CTF_Team2,4);
		specialeffect 73;
		emotion ET_HELP,getcharid(3);
		addtimer 2100, "Flavius_CTF::OnOmegaFlash";
		percentheal -5,-5;
	}
	end;

OnInit:
	if (getbattleflag("bg_queue_interface")) {
		.T1$ = "$@BG_CTF_Team1";
		.T2$ = "$@BG_CTF_Team2";
	} else {
		.T1$ = "$@BG_Team1";
		.T2$ = "$@BG_Team2";
	}
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";
	end;

OnTeam1Quit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join")*1000,0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_CTF != 0)
		donpcevent "CTF_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_CTF")
		donpcevent "BG_Queue_Join::OnJoinEvent";
	if ($@BG_Team1 && bg_get_data($@BG_Team1,0) == 0)
		donpcevent "Flavius_CTF::OnMatchEnd";
	if ($@BG_CTF_Team1 && bg_get_data($@BG_CTF_Team1,0) == 0)
		donpcevent "Flavius_CTF::OnMatchEnd";
OnTeam1Die:
	// Drop Flag
	if (($@BG_Status == 1 || $@BG_Status_CTF) && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_CTF" && getvariableofnpc(.Flag_Carrier,"Team2_Flag") == getcharid(0)) {
		set getvariableofnpc(.Flag_Carrier,"Team2_Flag"), 0;
		setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
		getmapxy .@m$, .@x, .@y, BL_PC;
		movenpc "Team2_Flag", .@x, .@y;
		if ($@BG_Team2)
			mapannounce "bat_b02","Omega Flag Droped by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_Team2,4);
		else
			mapannounce "bat_b02","Omega Flag Droped by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_CTF_Team2,4);
		bg_rankpoints "ctf_droped",1,0;
		if (killerrid)
			bg_rankpoints "fame",0,1,killerrid;
		set getvariableofnpc(.Flag_Status,"Team2_Flag"), 1; // OnFloor
		initnpctimer "Team2_Flag";
		enablenpc "Team2_Flag";
	}
	end;
	
OnTeam2Quit:
	sc_start(SC_ENTRY_QUEUE_APPLY_DELAY,getvariableofnpc(.DeserterCD,"BG_Queue_Join")*1000,0); // Deserter
	if (getbattleflag("bg_queue_interface") && $@BG_Status_CTF != 0)
		donpcevent "CTF_BG_Queue::OnJoinEvent";
	if (!getbattleflag("bg_queue_interface") && $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_CTF")
		donpcevent "BG_Queue_Join::OnJoinEvent";
	if ($@BG_Team2 && bg_get_data($@BG_Team2,0) == 0)
		donpcevent "Flavius_CTF::OnMatchEnd";
	if ($@BG_CTF_Team2 && bg_get_data($@BG_CTF_Team2,0) == 0)
		donpcevent "Flavius_CTF::OnMatchEnd";
OnTeam2Die:
	// Drop Flag
	if (($@BG_Status == 1 || $@BG_Status_CTF) && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_CTF" && getvariableofnpc(.Flag_Carrier,"Team1_Flag") == getcharid(0)) {
		set getvariableofnpc(.Flag_Carrier,"Team1_Flag"), 0;
		setpcblock(PCBLOCK_SKILL|PCBLOCK_USEITEM,false);
		getmapxy .@m$, .@x, .@y, BL_PC;
		movenpc "Team1_Flag", .@x, .@y;
		if ($@BG_Team1)
			mapannounce "bat_b02","Alpha Flag Droped by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_Team1,4);
		else
			mapannounce "bat_b02","Alpha Flag Droped by [ " + strcharinfo(0) + " ]",1,bg_get_data($@BG_CTF_Team1,4);
		bg_rankpoints "ctf_droped",1,0;
		if (killerrid)
			bg_rankpoints "fame",0,1,killerrid;
		set getvariableofnpc(.Flag_Status,"Team1_Flag"), 1; // OnFloor
		initnpctimer "Team1_Flag";
		enablenpc "Team1_Flag";
	}
	end;

OnBuildTeams:
	if (getbattleflag("bg_queue_interface")) {
		set $@BG_CTF_Team1,bg_create("bat_b02",390,10,0,"Flavius_CTF::OnTeam1Quit","Flavius_CTF::OnTeam1Die");
		set $@BG_CTF_Team2,bg_create("bat_b02",10,290,1,"Flavius_CTF::OnTeam2Quit","Flavius_CTF::OnTeam2Die");
	} else {
		set $@BG_Team1,bg_create("bat_b02",390,10,0,"Flavius_CTF::OnTeam1Quit","Flavius_CTF::OnTeam1Die");
		set $@BG_Team2,bg_create("bat_b02",10,290,1,"Flavius_CTF::OnTeam2Quit","Flavius_CTF::OnTeam2Die");
	}
	end;

OnReady:
	if (getbattleflag("bg_queue_interface") == 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_CTF")
		end;

	initnpctimer;
	// BG Variables
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 2100;
	if (getbattleflag("bg_queue_interface")) {
		bg_warp $@BG_CTF_Team1,"bat_b02",311,224;
		bg_warp $@BG_CTF_Team2,"bat_b02",87,75;
	} else {
		bg_warp $@BG_Team1,"bat_b02",311,224;
		bg_warp $@BG_Team2,"bat_b02",87,75;
	}
	sleep 2100;
	// Respawn NPC's
	donpcevent "#guictf_respawn::OnBGStart";
	donpcevent "#croctf_respawn::OnBGStart";
	// Start Match!!
	donpcevent "Flavius_CTF::OnMatchStart";
	end;

OnMatchStart:
	// Reset Position Members
	if (.Team1_Score || .Team2_Score) {
		if (getbattleflag("bg_queue_interface")) {
			bg_warp $@BG_CTF_Team1,"bat_b02",311,224;
			bg_warp $@BG_CTF_Team2,"bat_b02",87,75;
		} else {
			bg_warp $@BG_Team1,"bat_b02",311,224;
			bg_warp $@BG_Team2,"bat_b02",87,75;
		}
	}

	// Flags2Base
	donpcevent "Team1_Flag::OnBase";
	donpcevent "Team2_Flag::OnBase";
	mapannounce "bat_b02","The Flags have been set to their Bases!!",8;
	end;

OnTeam1Score:
	set .Team1_Score, .Team1_Score + 1;
	donpcevent "Flavius_CTF::OnMatchStop";
	end;

OnTeam2Score:
	set .Team2_Score, .Team2_Score + 1;
	donpcevent "Flavius_CTF::OnMatchStop";
	end;

OnMatchStop:
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";
	bg_updatescore "bat_b02",.Team1_Score,.Team2_Score;

	viewpointmap "bat_b02",2, 0, 0, 1, 0x0000FF;
	viewpointmap "bat_b02",2, 0, 0, 2, 0xFF0000;

	// Team 1 Won
	if (.Team1_Score > 2) {
		if (getbattleflag("bg_queue_interface"))
			mapannounce "bat_b02","The " + bg_get_data($@BG_CTF_Team1,2) + " army has won the Battle of Flavius CTF!",1,bg_get_data($@BG_CTF_Team1,4);
		else
			mapannounce "bat_b02","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius CTF!",1,bg_get_data($@BG_Team1,4);
		donpcevent "Flavius_CTF::OnMatchEnd";
	}
	// Team 2 Won
	else if (.Team2_Score > 2) {
		if (getbattleflag("bg_queue_interface"))
			mapannounce "bat_b02","The " + bg_get_data($@BG_CTF_Team2,2) + " army has won the Battle of Flavius CTF!",1,bg_get_data($@BG_CTF_Team2,4);
		else
			mapannounce "bat_b02","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius CTF!",1,bg_get_data($@BG_Team2,4);
		donpcevent "Flavius_CTF::OnMatchEnd";
	}
	// Keep Playing
	else {
		sleep 8000;
		donpcevent "Flavius_CTF::OnMatchStart";
	}
	end;

OnTimer600000:
	mapannounce "bat_b02","The Battle will ends in 5 minutes!!",1,0xA0522D;
	end;

OnTimer840000:
	mapannounce "bat_b02","The Battle will ends in 1 minute!!",1,0xA0522D;
	end;

OnTimer900000:
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";

	viewpointmap "bat_b02",2, 0, 0, 1, bg_get_data(getd(.T1$),4);
	viewpointmap "bat_b02",2, 0, 0, 2, bg_get_data(getd(.T2$),4);

	if (.Team1_Score > .Team2_Score)
		mapannounce "bat_b02","The " + bg_get_data(getd(.T1$),2) + " army has won the Battle of Flavius CTF!",1,bg_get_data(getd(.T1$),4);
	else if (.Team1_Score < .Team2_Score)
		mapannounce "bat_b02","The " + bg_get_data(getd(.T2$),2) + " army has won the Battle of Flavius CTF!",1,bg_get_data(getd(.T2$),4);
	else
		mapannounce "bat_b02","The battle is over. This is a Tie...!",1,0xA0522D;
	donpcevent "Flavius_CTF::OnMatchEnd";
	end;

OnMatchEnd:
	stopnpctimer;
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";
	donpcevent "#guictf_respawn::OnBGStop";
	donpcevent "#croctf_respawn::OnBGStop";
	if (getbattleflag("bg_queue_interface"))
		set $@BG_Status_CTF, 2;
	else
		set $@BG_Status, 2;
	// =======================================================
	// Team Rewards
	// =======================================================
	.@reward_win = 5;
	.@reward_lose = 3;
	if (.Team1_Score || .Team2_Score) {
		if (.Team1_Score > .Team2_Score) {
			bg_reward getd(.T1$),7829,.@reward_win,0,0,"BG_CTF",1,3,0;
			bg_reward getd(.T2$),7829,.@reward_lose,0,0,"BG_CTF",1,3,2;
		} else if (.Team2_Score > .Team1_Score) {
			bg_reward getd(.T1$),7829,.@reward_lose,0,0,"BG_CTF",1,3,2;
			bg_reward getd(.T2$),7829,.@reward_win,0,0,"BG_CTF",1,3,0;
		} else {
			bg_reward getd(.T1$),7829,.@reward_lose,0,0,"BG_CTF",1,3,1;
			bg_reward getd(.T2$),7829,.@reward_lose,0,0,"BG_CTF",1,3,1;
		}
	}
	// =======================================================
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 5000;
	donpcevent "BG_Queue_Join::OnEndArena";
	bg_updatescore "bat_b02",0,0;
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b02,390,13,5	script	Therapist in battle#ctf1	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

bat_b02,10,293,5	script	Therapist in battle#ctf2	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, and take a deep breathe.";
	mes "You can be free from pain.";
	specialeffect 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

// Battleground Respawn
// *********************************************************************

bat_b02,390,10,0	script	#guictf_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",382,2,397,17,100,100;
	areawarp "bat_b02",382,2,397,17,"bat_b02",311,224;
	initnpctimer;
	end;
}

bat_b02,10,290,0	script	#croctf_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",2,282,17,297,100,100;
	areawarp "bat_b02",2,282,17,297,"bat_b02",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b02,304,231,1	duplicate(Base Flag#bg)	Alpha Base#bat23	1_FLAG_LION
bat_b02,319,231,1	duplicate(Base Flag#bg)	Alpha Base#bat24	1_FLAG_LION
bat_b02,304,218,1	duplicate(Base Flag#bg)	Alpha Base#bat25	1_FLAG_LION
bat_b02,319,218,1	duplicate(Base Flag#bg)	Alpha Base#bat26	1_FLAG_LION
bat_b02,304,231,1	duplicate(Base Flag#bg)	Alpha Base#bat27	1_FLAG_LION
bat_b02,304,231,1	duplicate(Base Flag#bg)	Alpha Base#bat28	1_FLAG_LION
bat_b02,335,142,1	duplicate(Base Flag#bg)	Alpha Base#bat29	1_FLAG_LION
bat_b02,335,157,1	duplicate(Base Flag#bg)	Alpha Base#bat30	1_FLAG_LION
bat_b02,390,16,1	duplicate(Base Flag#bg)	Alpha Base#bat31	1_FLAG_LION
bat_b02,292,163,1	duplicate(Base Flag#bg)	Alpha Base#bat32	1_FLAG_LION
bat_b02,292,136,1	duplicate(Base Flag#bg)	Alpha Base#bat33	1_FLAG_LION
bat_b02,241,185,1	duplicate(Base Flag#bg)	Alpha Base#bat34	1_FLAG_LION
bat_b02,247,179,1	duplicate(Base Flag#bg)	Alpha Base#bat35	1_FLAG_LION

bat_b02,96,81,1	duplicate(Base Flag#bg)	Omega Base#bat22	1_FLAG_EAGLE
bat_b02,96,68,1	duplicate(Base Flag#bg)	Omega Base#bat23	1_FLAG_EAGLE
bat_b02,79,81,1	duplicate(Base Flag#bg)	Omega Base#bat24	1_FLAG_EAGLE
bat_b02,79,68,1	duplicate(Base Flag#bg)	Omega Base#bat25	1_FLAG_EAGLE
bat_b02,96,81,1	duplicate(Base Flag#bg)	Omega Base#bat26	1_FLAG_EAGLE
bat_b02,96,81,1	duplicate(Base Flag#bg)	Omega Base#bat27	1_FLAG_EAGLE
bat_b02,59,164,1	duplicate(Base Flag#bg)	Omega Base#bat28	1_FLAG_EAGLE
bat_b02,59,137,1	duplicate(Base Flag#bg)	Omega Base#bat29	1_FLAG_EAGLE
bat_b02,10,296,1	duplicate(Base Flag#bg)	Omega Base#bat30	1_FLAG_EAGLE
bat_b02,110,162,1	duplicate(Base Flag#bg)	Omega Base#bat31	1_FLAG_EAGLE
bat_b02,110,137,1	duplicate(Base Flag#bg)	Omega Base#bat32	1_FLAG_EAGLE
bat_b02,152,120,1	duplicate(Base Flag#bg)	Omega Base#bat33	1_FLAG_EAGLE
bat_b02,158,114,1	duplicate(Base Flag#bg)	Omega Base#bat34	1_FLAG_EAGLE

// MapFlags
// *********************************************************************

bat_b02	mapflag	battleground	2
bat_b02	mapflag	nomemo
bat_b02	mapflag	nosave	SavePoint
bat_b02	mapflag	noteleport
bat_b02	mapflag	nowarp
bat_b02	mapflag	nowarpto
bat_b02	mapflag	noreturn
bat_b02	mapflag	nobranch
bat_b02	mapflag	nopenalty
bat_b02	mapflag	noecall
bat_b02	mapflag	bg_consume
